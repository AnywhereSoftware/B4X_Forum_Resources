Build1=Default,B4RDev
Group=Default Group
Library1=rcore
Library2=rlovyangfxex
NumberOfFiles=0
NumberOfLibraries=2
NumberOfModules=0
Version=4
@EndOfDesignText@
#Region Project Notes
'Project:		rLovyanGFXEx
'Description:	Example showing a 7-Segment-Display using the font type FONT7.
'Date:			See globals version
'Author:		Robert W.B. Linn

'Additional Libraries used (min version)

'Microcontroller
'ESP32 Dev Module
'IMPORTANT: Change Tools > Board Selector > Partition scheme to "Hugh app (3MB no OTA/ 1MB SPIFFS)" 

'Compile
'Reset the ESP32 after compile (press reset button).

'Wiring
'No additional wiring

'Ctrl+Click to open the C code folder: ide://run?File=%WINDIR%\System32\explorer.exe&Args=%PROJECT%\Objects\Src
#End Region

#Region Project Attributes
	#AutoFlushLogs: True
	#CheckArrayBounds: True
	#StackBufferSize: 900
#End Region

Sub Process_Globals
	Private VERSION As String = "rLovyanGFXEx 7-SEGMENT-DISPLAY v20250406"

	'Communication
	Public Serial1 As Serial

	'Display Instance
	Private lcd As LovyanGFXEx
	
	'Timer as counter
	Private TimerCounter As Timer
	Private TimerCounterInterval As Long = 1000
	Private TimerCounterValue As ULong = 0
	Private TimerCounterReset As ULong = 99
End Sub

Private Sub AppStart
	Serial1.Initialize(115200)
	Log(CRLF, CRLF, "[AppStart]", VERSION)

	'Init display without touch
	lcd.Initialize(Null)

	'Timer Counter
	TimerCounter.Initialize("TimerCounter_Tick", TimerCounterInterval)
	TimerCounter.Enabled = False

	' Callsub to let the display initialize
	CallSubPlus("SetUp", 500, 5)
End Sub

Private Sub SetUp(tag As Byte)
	lcd.DisplayRotation = 1

	'lcd.FillScreen(lcd.COLOR_WHITE)
	lcd.BackgroundColor = lcd.color_white

	'Draw version text at top middle center
	lcd.DrawHeader(VERSION, lcd.FONT_FONT2, lcd.TEXT_ALIGN_CENTER, 1, lcd.COLOR_BLACK, lcd.COLOR_WHITE_SMOKE, lcd.COLOR_BLACK)

	'Draw info text at the bottom
	lcd.DrawFooter("Counter resets at 9999.", lcd.FONT_FONT0, lcd.TEXT_ALIGN_LEFT, 1, lcd.COLOR_BLACK, lcd.BackgroundColor, lcd.COLOR_NONE)
	
	TimerCounter.Enabled = True
End Sub

Private Sub TimerCounter_Tick
	'Dimensions for the 7-segment-display
	Dim x,y,w,h As UInt
	'Font
	Dim fontsize As Byte = 2
	
	'Increase timer value
	TimerCounterValue = TimerCounterValue + 1
	
	'Reset timercountervalue
	If TimerCounterValue > TimerCounterReset Then TimerCounterValue = 0

	'Set the 7-segment-display font
	lcd.FONT = lcd.FONT_FONT7

	'Set the diemensions for the area to clear
	x = 0
	y = lcd.CenterY - ( (lcd.GetFontHeight * fontsize) / 2)
	w = lcd.width
	h = lcd.GetFontHeight * fontsize
	lcd.ClearArea(x, y, w, h, lcd.BackgroundColor)

	'Draw the text using the 7-segment-display font
	lcd.DrawText(lcd.CenterX, lcd.CenterY, NumberFormat(TimerCounterValue, 0, 0), lcd.TEXT_ALIGN_MIDDLE_CENTER, fontsize, lcd.COLOR_BLACK, lcd.COLOR_NONE)
End Sub
