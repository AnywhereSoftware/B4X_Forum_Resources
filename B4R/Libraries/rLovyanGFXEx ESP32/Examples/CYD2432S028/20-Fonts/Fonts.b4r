Build1=Default,B4RDev
Group=Default Group
Library1=rcore
Library2=rlovyangfxex
NumberOfFiles=0
NumberOfLibraries=2
NumberOfModules=0
Version=4
@EndOfDesignText@
#Region Project Notes
'Project:		rLovyanGFXEx
'Description:	Example showing some fonts.
'Date:			See globals version
'Author:		Robert W.B. Linn

'Additional Libraries used (min version)

'Microcontroller
'ESP32 Dev Module
'IMPORTANT: Change Tools > Board Selector > Partition scheme to "Hugh app (3MB no OTA/ 1MB SPIFFS)" 

'Compile
'Reset the ESP32 after compile (press reset button).

'Wiring
'No additional wiring

'Ctrl+Click to open the C code folder: ide://run?File=%WINDIR%\System32\explorer.exe&Args=%PROJECT%\Objects\Src
#End Region

#Region Project Attributes
	#AutoFlushLogs: True
	#CheckArrayBounds: True
	#StackBufferSize: 600
#End Region

Sub Process_Globals
	Private VERSION As String = "rLovyanGFXEX FONT SELECTION v20250409"

	' Communication
	Public Serial1 As Serial

	' Display instance
	Private lcd As LovyanGFXEx

	' Text position
	Dim x, y As ULong
End Sub

Private Sub AppStart
	Serial1.Initialize(115200)
	Log(CRLF, CRLF, "[AppStart]", VERSION)

	'Init display without touch
	lcd.Initialize(Null)

	'Call method demo
	CallSubPlus("Demo", 500, 5)
End Sub

Private Sub Demo(tag As Byte)
	' Set screen rotation to power top left.
	lcd.DisplayRotation = 1
	
	' Set screen background color
	lcd.BackgroundColor = lcd.COLOR_WHITE

	' Draw version text at top middle center
	lcd.DrawHeader(VERSION, lcd.FONT_FONT2, lcd.TEXT_ALIGN_CENTER, 1, lcd.COLOR_BLACK, lcd.COLOR_LIGHTGRAY, lcd.COLOR_BLACK)

	' Set starting coordinates
	y = 20

	'Show some fonts
	ShowFont(lcd.FONT_DEFAULT, 			"FONT0 - default", 		1, lcd.COLOR_RED)
	ShowFont(lcd.FONT_DEFAULT, 			"FONT0 - size 2", 		2, lcd.COLOR_BLUE)
	ShowFont(lcd.FONT_FREEMONO_12, 		"FREEMONO_12", 			1, lcd.COLOR_MAGENTA)
	ShowFont(lcd.FONT_FREEMONOBOLD_12,	"FREEMONOBOLD_12", 		1, lcd.COLOR_RED)
	ShowFont(lcd.FONT_FREESANS_12, 		"FREESANS_12", 			1, lcd.COLOR_BLACK)
	ShowFont(lcd.FONT_FREESANSBOLD_12, 	"FREESANSBOLD_12", 		1, lcd.COLOR_GREEN)
	ShowFont(lcd.FONT_TOMTHUMB_1, 		"TOMTHUMB", 			1, lcd.COLOR_ILLUMINATING)
	ShowFont(lcd.FONT_FREESERIF_12, 	"FREESERIF_12", 		1, lcd.COLOR_ORANGE)
	ShowFont(lcd.FONT_ORBITRON_LIGHT_24,"ORBITRON_LIGHT_24",	1, lcd.COLOR_DARKRED)
	ShowFont(lcd.FONT_FONT7,			"7777",					1, lcd.COLOR_BLACK)

	'Set default font
	lcd.Font = lcd.FONT_DEFAULT
End Sub

Private Sub ShowFont(fonttype As Byte, fontinfo As String, fontsize As Double, color As ULong)
	' Font height
	Dim h As Int
	
	'Set the font type
	lcd.Font = fonttype

	'Set the new y pos based on the selected font height
	h = (lcd.FontHeight * fontsize) / 2
	x = 10
	y = y + h

	'Draw fontinfo text using the fonttype
	'lcd.DrawText(x, y, fontinfo, lcd.TEXT_ALIGN_MIDDLE_CENTER, fontsize, color, lcd.BackgroundColor)
	
	'Draw classic
	lcd.SetTextColor(color)
	lcd.SetTextSize(fontsize)
	lcd.SetTextDatum(lcd.TEXT_ALIGN_MIDDLE_LEFT)
	lcd.DrawString(fontinfo, x, y)
	Log("[ShowFont] x=",x, ",y=", y)

	'Draw a line below the font
	h = (lcd.FontHeight * fontsize) / 2
	y = y + h
	lcd.DrawLine(0, y, lcd.Width, y, lcd.COLOR_LIGHTGRAY)

	'Show font info
	Log("[ShowFont]", fontinfo, ",#base h=", lcd.FontHeight,",w=",lcd.FontWidth, ",#size h=", lcd.FontHeight * fontsize,",w=",lcd.FontWidth * fontsize, ",#w2=",lcd.FontWidthX(fontsize))
End Sub

