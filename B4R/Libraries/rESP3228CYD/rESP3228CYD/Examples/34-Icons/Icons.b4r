Build1=Default,B4RDev
Group=Default Group
Library1=rcore
Library2=rrandomaccessfile
Library3=resp3228cyd
Module1=Icons
NumberOfFiles=0
NumberOfLibraries=3
NumberOfModules=1
Version=4
@EndOfDesignText@
#Region Project Notes
'Project:		rESP3228CYD
'Description:	B4R Library rESP3228CYD example GPIO.
'Date:			See globals version
'Author:		Robert W.B. Linn

'Additional Libraries used (min version)
'rRandomAccessFile 1.91 - ByteConverter

'Microcontroller
'ESP32 Dev Module
'IMPORTANT: Change Tools > Board Selector > Partition scheme to "Hugh app (3MB no OTA/ 1MB SPIFFS)" 

'Compile
'Reset the ESP32 after compile (press reset button).

'Wiring
'No additional wiring
#End Region

#Region Project Attributes
	#AutoFlushLogs: True
	#CheckArrayBounds: True
	#StackBufferSize: 1200
#End Region
'Ctrl+Click to open the C code folder: ide://run?File=%WINDIR%\System32\explorer.exe&Args=%PROJECT%\Objects\Src

Sub Process_Globals
	Private VERSION As String = "rESP3228CYD Example Icons v20250316"

	'Communication
	Public Serial1 As Serial
	Private lcd As ESP3228CYD

	'Helper
	Private bc As ByteConverter	'ignore
End Sub

Private Sub AppStart
	Serial1.Initialize(115200)
	Log(CRLF, CRLF, "[AppStart]", VERSION)

	'Init display without touch
	lcd.Initialize(Null)

	'Call method demo
	CallSubPlus("Demo", 500, 5)
End Sub

Private Sub Demo(tag As Byte)

	Log("[Demo] Display dimensions: w=", lcd.Width,",h=",lcd.Height, ",Font h=", lcd.FontHeight(1),",w=",lcd.FontWidth(1))

	lcd.DisplayRotation = 1
	Log("[Demo] getrotation=", lcd.DisplayRotation)
	
	'Set screen background color green
	lcd.BackgroundColor = lcd.COLOR_GREEN
	Log("[Demo] backgroundcolor=", bc.HexFromBytes(bc.ULongsToBytes(Array As ULong(lcd.BackgroundColor))))

	'Draw version text at top middle center
	lcd.DrawText(lcd.Width / 2, 10, VERSION, lcd.TEXT_ALIGN_MIDDLE_CENTER, 1.5, lcd.COLOR_BLUE, lcd.COLOR_WHITE)

	'Draw text bottom left using drawtext
	lcd.DrawText(10, lcd.Height - lcd.FontHeight(1) - 2, "B4X is great... enjoy", lcd.TEXT_ALIGN_MIDDLE_LEFT, 1, lcd.COLOR_BLACK, lcd.COLOR_DEFAULT)

	'Draw some test icons defined in the code module icons.
	lcd.DrawIcon(20,50, 6, 8, 8, Icons.BatteryFull, lcd.COLOR_BLACK, lcd.color_GREEN)
	lcd.DrawIcon(100,50, 6, 8, 8, Icons.BatteryHalf, lcd.COLOR_BLACK, lcd.color_YELLOW)
	lcd.DrawIcon(180,50, 4, 8, 8, Icons.BatteryEmpty, lcd.COLOR_BLACK, lcd.color_RED)

	lcd.DrawIcon(20,150, 5, 8, 8, Icons.ArrowRight, lcd.COLOR_BLACK, lcd.BackgroundColor)
	lcd.DrawIcon(100,150, 5, 8, 4, Icons.ArrowRight, lcd.COLOR_BLUE, lcd.BackgroundColor)
	lcd.DrawIcon(180,150, 5, 8, 16, Icons.ArrowRight, lcd.COLOR_RED, lcd.BackgroundColor)

End Sub

'Example own method to draw a pixel size 8x8
Public Sub DrawPixel8(x As Int, y As Int, color As ULong)
	For i=x To x+8
		For j=y To y+8
			lcd.DrawPixel(i, j, color)
		Next
	Next
End Sub

'Example own method to draw an icon
'DrawIcon(100, 100, 6, 8, 4, SpecialChars.BatteryHalf, lcd.COLOR_BLACK, lcd.color_RED)
Public Sub DrawIcon(x As UInt, y As UInt, rows As UInt, cols As UInt, size As Byte, bitmap() As Byte, color As ULong, bgcolor As ULong)
	Dim row As Int
	Dim col As Int
	Dim clr As ULong
	
	For row = 0 To rows - 1
		For col = 0 To cols - 1
			clr = IIf(Bit.And(Icons.BatteryHalf(row), Bit.ShiftRight(0x80, col)) <> 0, color, bgcolor)
			lcd.DrawPixelSize(x + col * size, y + row * size, size, clr)
		Next
	Next
End Sub

'Example own method to draw an icon BatteryHalf
Public Sub DrawBatteryHalf(x As Long, y As Long, color As ULong)
	Dim row As Int
	Dim col As Int
	For row = 0 To 5 ' 6 rows total
		For col = 0 To 7 ' 8 columns total
			If Bit.And(Icons.BatteryHalf(row), Bit.ShiftRight(0x80, col)) <> 0 Then ' Shift RIGHT instead
				lcd.DrawPixel4(x + col * 4, y + row * 4, color) ' 4x4 blocks
			End If
		Next
	Next
End Sub
