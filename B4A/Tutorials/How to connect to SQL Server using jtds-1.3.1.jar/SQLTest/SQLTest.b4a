Build1=Default,b4a.example
File1=Main.bal
FileGroup1=Default Group
Group=Default Group
IconFile=
Library1=core
Library2=javaobject
Library3=jdbcsql
Library4=preferenceactivity
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: https://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="5" android:targetSdkVersion="28"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~
NumberOfFiles=1
NumberOfLibraries=4
NumberOfModules=0
Version=9.01
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: SQL Example
	#VersionCode: 1
	#VersionName: 
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: unspecified
	#CanInstallToExternalStorage: False
	#AdditionalJar: jtds-1.3.1.jar
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: True
	#BridgeLogger: True
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	Dim sql1 As JdbcSQL

End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
	'These variables can only be accessed from this module.
	Dim ConnStr, DBName, UserName, PasswordTb As EditText
	Dim SQLText As EditText
	Dim Button2 As Button
	Dim Pref As PreferenceManager
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Dim A As String
Activity.LoadLayout("Main")
Button2.Enabled = False
	If FirstTime = True Then 
		DisableStrictMode
	End If
	A = Pref.GetString("ConnStr")
	If A.Length > 0 Then ConnStr.Text = A
	A = Pref.GetString("DBName")
	If A.Length > 0 Then DBName.Text = A
	A = Pref.GetString("UserName")
	If A.Length > 0 Then UserName.Text = A
	A = Pref.GetString("PasswordTb")
	If A.Length > 0 Then PasswordTb.Text = A
	A = Pref.GetString("SQLText")
	If A.Length > 0 Then SQLText.Text = A

End Sub


Sub sql1_Ready (Success As Boolean)
	Try
		ProgressDialogHide
		Log(Success)
		If Success = False Then
			Log("Connection Failed")
			MsgboxAsync("Connection Failed" & CRLF & LastException,"")
			Log(LastException)
			Return
		End If
		Button2.Enabled = True
		MsgboxAsync("Connection Successful","")
	Catch
		Log(LastException)
	End Try
End Sub


Sub Button1_Click
	Try
		
		'sql1.InitializeAsync("Connect", "net.sourceforge.jtds.jdbc.Driver", "jdbc:jtds:sqlserver:" & ConnectStr & ";databaseName=" & DBName & ";user=skdata;password=shopkeeper;appname=SKMJL;wsid=" & S & ";loginTimeout=10", "skdata", "shopkeeper")  'sql 2012
		sql1.InitializeAsync("sql1", "net.sourceforge.jtds.jdbc.Driver", "jdbc:jtds:sqlserver:" & ConnStr.Text & ";databaseName=" & DBName.Text & ";user=" & UserName.Text & ";password=" & PasswordTb.Text & ";appname=SKMJL;wsid=TEST;loginTimeout=10;socketTimeout=10", "", "")  'sql 2012
ProgressDialogShow("Trying to Connect")
	Catch
		Log("connection error ")
		Log(LastException.Message)
		Return
	End Try

End Sub

Sub Button2_Click
	Dim A As String, X As Int
	Try
		sql1.ExecQueryAsync("QryTest", SQLText.Text, Null)
		ProgressDialogShow("Executing Query")
		Wait For QryTest_QueryComplete (Success As Boolean, Crsr As JdbcResultSet)
		ProgressDialogHide
		If Success = True Then
			Do While Crsr.NextRow
				A = A & CRLF & Crsr.GetString2(0)
				X=X+1
				Sleep(0)
				Log(X)
				If X=10 Then Exit
			Loop
			Crsr.Close
			MsgboxAsync("Query Successfull! Top " & X & " Values for Field 0" & CRLF & A, "")
		Else
			MsgboxAsync("Query Failed","")
		End If
	Catch
		Log(LastException)
		MsgboxAsync(LastException,"")
	End Try
End Sub

Sub Activity_Resume

End Sub

Sub Activity_Pause (UserClosed As Boolean)
	Pref.setString("ConnStr", ConnStr.Text)
	Pref.setString("DBName", DBName.Text)
	Pref.setString("UserName", UserName.Text)
	Pref.setString("PasswordTb", PasswordTb.Text)
	Pref.setString("SQLText", SQLText.Text)

End Sub

Sub DisableStrictMode
	Dim jo As JavaObject
	jo.InitializeStatic("android.os.Build.VERSION")
	If jo.GetField("SDK_INT") > 9 Then
		Dim policy As JavaObject
		policy = policy.InitializeNewInstance("android.os.StrictMode.ThreadPolicy.Builder", Null)
		policy = policy.RunMethodJO("permitAll", Null).RunMethodJO("build", Null)
		Dim sm As JavaObject
		sm.InitializeStatic("android.os.StrictMode").RunMethod("setThreadPolicy", Array(policy))
	End If
End Sub
