Version=4.2
AppType=JavaFX
NumberOfModules=1
Module1=Localizator
Build1=Default,b4j.example
NumberOfFiles=2
File1=1.bjl
File2=strings.db
NumberOfLibraries=4
Library1=jcore
Library2=jfx
Library3=jsql
Library4=javaobject
@EndOfDesignText@
#Region  Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 400 
#End Region
#AdditionalJar: sqlite-jdbc-3.7.2
Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Public loc As Localizator
	Private lblHello As Label
	Private lstLanguages As ListView
	Private name As String
	Private edtName As TextField
	Private availableLangs() As String = Array As String ("en", "fr", "he", "de")
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	loc.Initialize(File.DirAssets, "strings.db")
	MainForm = Form1
	ResetLayout
	MainForm.Show
End Sub

Sub ResetLayout
	If edtName.IsInitialized Then name = edtName.Text
	MainForm.RootPane.RemoveAllNodes
	MainForm.RootPane.LoadLayout("1") 'Load the layout file.
	If loc.Language = "he" Then
		'change the orientation to RTL
		MainForm.RootPane.Orientation = MainForm.RootPane.ORIENTATION_RIGHT_TO_LEFT
	End If
	loc.LocalizeLayout(MainForm.RootPane)
	For i = 0 To availableLangs.Length - 1
		lstLanguages.Items.Add(loc.Localize(availableLangs(i)))
	Next
	edtName.Text = name
	UpdateNameLabel
End Sub

Sub edtName_TextChanged (Old As String, New As String)
	UpdateNameLabel
End Sub

Sub UpdateNameLabel
	lblHello.Text = loc.LocalizeParams("Hello {1}!", Array(edtName.Text))
End Sub


Sub lstLanguages_SelectedIndexChanged(Index As Int)
	loc.ForceLocale(availableLangs(Index))
	ResetLayout
End Sub