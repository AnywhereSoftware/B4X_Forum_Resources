AppType=JavaFX
Build1=Default,b4j.example
File1=1.bjl
File2=Cell.bjl
FileGroup1=Default Group
FileGroup2=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jxui
Library4=xcustomlistview
NumberOfFiles=2
NumberOfLibraries=4
NumberOfModules=0
Version=7.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private CustomListView1 As CustomListView
	Type CellData (Field1 As String, Field2 As String, IsVisible As Boolean)
	Private Layouts As List
	Private xui As XUI
	Private btnList As B4XView
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1") 'Load the layout file.
	MainForm.Show
	Layouts.Initialize
	For i = 1 To 100
		Dim p As B4XView = xui.CreatePanel("")
		p.SetLayoutAnimated(0, 0, 0, CustomListView1.AsView.Width, 90dip)
		Dim cd As CellData
		cd.Initialize
		cd.Field1 = $"Item ${i}"$
		CustomListView1.Add(p, cd)
	Next
End Sub

Sub CustomListView1_VisibleRangeChanged (FirstIndex As Int, LastIndex As Int)
	For i = 0 To CustomListView1.Size - 1
		Dim cd As CellData = CustomListView1.GetValue(i)
		Dim pnl As B4XView = CustomListView1.GetPanel(i)
		If i >= FirstIndex - 2 And i <= LastIndex + 2 Then
			'visible
			If cd.IsVisible = False Then
				AddCellLayout(pnl)
				cd.IsVisible = True
				Dim content As B4XView = pnl.GetView(0)
				content.GetView(0).Text = cd.Field1
				content.GetView(1).Text = cd.Field2
			End If
		Else
			'invisible
			If cd.IsVisible Then
				Dim content As B4XView = pnl.GetView(0)
				content.RemoveViewFromParent
				Layouts.Add(content)
				cd.IsVisible = False
				cd.Field1 = content.GetView(0).Text
				cd.Field2 = content.GetView(1).Text
			End If
		End If
		btnList.RequestFocus 'remove the focus from the text fields.
	Next
End Sub

Sub AddCellLayout (Parent As B4XView)
	If Layouts.Size > 0 Then
		Dim p As B4XView = Layouts.Get(Layouts.Size - 1)
		Layouts.RemoveAt(Layouts.Size - 1)
		Parent.AddView(p, 0, 0, Parent.Width, Parent.Height)
	Else
		Parent.LoadLayout("Cell")
		'note that the layout is made of a panel as the top view. This makes it easier to remove the content.
	End If
End Sub

Sub btnList_Click
	For i = 0 To CustomListView1.Size - 1
		Dim cd As CellData = CustomListView1.GetValue(i)
		
		Dim value1, value2 As String
		If cd.IsVisible = False Then
			value1 = cd.Field1
			value2 = cd.Field2
		Else
			'we need to get the values from the views
			Dim content As B4XView = CustomListView1.GetPanel(i).GetView(0)
			value1 = content.GetView(0).Text
			value2 = content.GetView(1).Text
		End If
		Log($"Value1: ${value1}, Value2: ${value2}"$)
	Next
End Sub