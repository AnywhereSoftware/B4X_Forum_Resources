Version=2.31
NumberOfModules=2
Module1=Connector
Module2=StateManager
Build1=Default,b4i.example1
NumberOfFiles=1
File1=1.bil
NumberOfLibraries=5
Library1=icore
Library2=imqtt
Library3=irandomaccessfile
Library4=ihud
Library5=itableview
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: B4i Example
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page
	Type Message (Body As String, From As String)
	Private btnConnect As Button
	Private btnSend As Button
	Private Label1 As Label
	Private pnlListBase As Panel
	Private txtHost As TextField
	Private txtLogs As TextView
	Private txtMessage As TextField
	Private txtName As TextField
	Private lstUsers As TableView
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
	NavControl.NavigationBarVisible = False
	Page1.Initialize("Page1")
	Page1.RootPanel.LoadLayout("1")
	NavControl.ShowPage(Page1)
	lstUsers.Initialize("lstUsers", False)
	pnlListBase.AddView(lstUsers, 0, 0, pnlListBase.Width, pnlListBase.Height)
	lstUsers.SeparatorColor = Colors.Transparent
	StateManager.RestoreState(Page1, "page1", 0)
	txtLogs.Text = ""
End Sub

Sub Page1_KeyboardStateChanged (Height As Float)
	txtLogs.Height = Page1.RootPanel.Height - Height - txtLogs.Top - 2
	pnlListBase.Height = Page1.RootPanel.Height - Height - pnlListBase.Top - 2
End Sub

Sub Page1_Click
	Page1.ResignFocus
End Sub


Sub pnlListBase_Resize (Width As Float, Height As Float)
	lstUsers.SetLayoutAnimated(0, 0.5, 0, 0, Width, Height)
End Sub


Sub btnSend_Click
	If txtMessage.Text <> "" Then Connector.SendMessage(txtMessage.Text)
	txtMessage.SelectAll
	txtMessage.RequestFocus
End Sub

Sub txtMessage_EnterPressed
	btnSend_Click
End Sub

Sub btnConnect_Click
	If txtName.Text = "" Then
		Msgbox("Please enter your name.", "")
		Return
	End If
	If txtHost.Text = "" Then
		Msgbox("Please enter the server ip address.", "")
		Return
	End If
	Connector.ConnectTo(txtHost.Text, txtName.Text)
End Sub

Public Sub NewMessage(msg As Message) 
	txtLogs.Text = $"${msg.From}: ${msg.Body}"$ & CRLF & txtLogs.Text
End Sub

Public Sub NewUsers(Users As List)
	lstUsers.Clear
	For Each u As String In Users
		Dim tc As TableCell = lstUsers.AddSingleLine("")
		Dim a As AttributedString
		a.Initialize(u, Font.CreateNew(13), Colors.Black)
		tc.Text = a
	Next
	lstUsers.ReloadAll
End Sub

Sub Application_Background
	StateManager.SaveState(Page1, "page1")
End Sub
