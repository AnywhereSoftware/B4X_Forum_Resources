AppType=JavaFX
Build1=Default,b4j.example
File1=1.bjl
File2=us_counties.csv
FileGroup1=Default Group
FileGroup2=New Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=xui views
Library4=b4xtable
Library5=jstringutils
Library6=b4xcollections
Module1=|absolute|C:\Users\H\Documents\B4J\B4XTableSelections
NumberOfFiles=2
NumberOfLibraries=6
NumberOfModules=1
Version=8.27
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private B4XComboBox1 As B4XComboBox
	Private B4XTable1 As B4XTable
	Private XSelections As B4XTableSelections
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1") 'Load the layout file.
	MainForm.Show
	B4XTable1.RowHeight = 40dip
	B4XTable1.AddColumn("US County", B4XTable1.COLUMN_TYPE_TEXT).Width = 80
	B4XTable1.AddColumn("Name", B4XTable1.COLUMN_TYPE_TEXT)
	B4XTable1.AddColumn("State", B4XTable1.COLUMN_TYPE_TEXT).Width = 80
	B4XTable1.AddColumn("Number Example", B4XTable1.COLUMN_TYPE_NUMBERS)
	Dim su As StringUtils
	Dim headers As List
	Dim data As List = su.LoadCSV2(File.DirAssets, "us_counties.csv", ",", headers)
	B4XTable1.SetData(data)
	XSelections.Initialize(B4XTable1)
	XSelections.Mode = XSelections.MODE_SINGLE_CELL_TEMP
	B4XComboBox1.SetItems(Array("SINGLE_CELL_TEMP", "SINGLE_CELL_PERMANENT", "SINGLE_LINE_PERMANENT", "MULTIPLE_CELLS", "MULTIPLE_LINES"))
	B4XComboBox1.DelayBeforeChangeEvent = 0
	'uncomment if you want selections to be removed automatically when the cell or line are not visible.
'	XSelections.AutoRemoveInvisibleSelections = True
End Sub

Sub B4XTable1_CellClicked (ColumnId As String, RowId As Long)
	XSelections.CellClicked(ColumnId, RowId)
End Sub

Sub B4XTable1_DataUpdated
	XSelections.Refresh
End Sub


Sub B4XComboBox1_SelectedIndexChanged (Index As Int)
	Dim mode As Int = Index + 1 'implementation detail. You should use the constants instead
	XSelections.Mode = mode
End Sub

Sub btnClear_Click
	XSelections.Clear
End Sub

Sub btnLog_Click
	If XSelections.Mode = XSelections.MODE_SINGLE_CELL_TEMP Then
		Log("Temporary mode. No selection")
		Return
	End If
	Dim LineMode As Boolean = XSelections.Mode = XSelections.MODE_MULTIPLE_LINES Or XSelections.Mode = XSelections.MODE_SINGLE_LINE_PERMANENT
	If XSelections.IsSelected = False Then
		Log("Nothing is selected")
	Else
		Log($"First selected row id: ${XSelections.FirstSelectedRowId}"$)
		If LineMode = False Then Log($"First selected column: ${XSelections.FirstSelectedColumnId}"$)
		Dim sb As StringBuilder
		sb.Initialize
		If LineMode Then
			sb.Append("The following rows are selected:").Append(CRLF)
			For Each rowid As Long In XSelections.SelectedLines.Keys
				sb.Append(rowid).Append(CRLF)
			Next
		Else
			sb.Append("The following cells are selected:").Append(CRLF)
			For Each rowid As Long In XSelections.SelectedLines.Keys
				Dim cols As List = XSelections.SelectedLines.Get(rowid)
				For Each col As String In cols
					sb.Append("(").Append(rowid).Append(", ").Append(col).Append(")").Append(CRLF)
				Next
			Next
		End If
		Log(sb.ToString)
	End If
End Sub
