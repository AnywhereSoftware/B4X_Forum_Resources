AppType=StandardJava
Build1=Default,b4j.example
Group=Default Group
Library1=b4xcollections
Library2=byteconverter
Library3=jcore
Library4=jrandomaccessfile
Library5=jshell
Library6=json
Library7=jstringutils
Library8=jokhttputils2
Module1=|absolute|C:\Users\H\Documents\B4J\JsonLayouts\BalConverter
NumberOfFiles=0
NumberOfLibraries=8
NumberOfModules=1
Version=10.3
@EndOfDesignText@
'Non-UI application (console / server application)
#Region Project Attributes 
	#CommandLineArgs:
	#MergeLibraries: True 
#End Region
#Macro: Title, Code bundle, ide://run?File=%ADDITIONAL%\..\B4X\CodeBundle.jar&Args=%PROJECT_NAME%&vmargs=-DDontShowExplorer%3DTrue
#CustomBuildAction: 2, %COMSPEC%, /c copy /Y "%PROJECT%\Objects\%PROJECT_NAME%.jar" %ADDITIONAL%\..\B4X\CodeBundle.jar"
Sub Process_Globals
	Private Version As Float = 0.2
	Private ProjectFile As String
	Private ProjectDir As String
	Private Platform As String
	Private AppType As String
	Private Libraries As List
	Private Modules As List
	Private MainModule As CodeModule
	Private ProjectName As String
	Type Parameter (Name As String, VarType As String)
	Type Member (MemberType As String, Name As String, Comment As String, ReturnType As String, Parameters As List)
	Type Class (Comment As String, Events As B4XSet, Members As Map)
	Private CurrentComment As String
	Type CodeModule (ModuleType As String, Code As String, Name As String, Headers As Map, Clazz As Class)
	Private HandleLayouts As Boolean
	Private CompactJson As Boolean
	Private Files As List
	Private RemoveApiKeys As Boolean
	Private LibrariesIndex As Map
End Sub

Sub AppStart (Args() As String)
	Start(Args)
	StartMessageLoop
End Sub

Private Sub DownloadLibrariesIndex As ResumableSub
	LibrariesIndex.Initialize
	Dim f As String = File.Combine(File.DirData("B4XCodeBundle"), "libraries_mapping.json")
	If File.Exists(f, "") = False Or File.LastModified(f, "") + 3 * DateTime.TicksPerDay < DateTime.Now Then
		Log("Downloading libraries index")
		Dim job As HttpJob
		job.Initialize("", Me)
		job.Download("https://raw.githubusercontent.com/AnywhereSoftware/B4X_Forum_Resources/refs/heads/main/libraries_mapping.json")
		Wait For (job) JobDone(job As HttpJob)
		If job.Success Then
			Dim out As OutputStream = File.OpenOutput(f, "", False)
			File.Copy2(job.GetInputStream, out)
			out.Close
		End If
		job.Release
	End If
	LibrariesIndex = File.ReadString(f, "").As(JSON).ToMap
	Return True
End Sub

Private Sub Start (Args() As String)
	Log($"Code bundle v$1.2{Version}"$)
	ProjectFile = File.Combine(File.DirApp, "..\" & Args(0))
	For Each extension As String In Array("", ".b4a", ".b4j", ".b4i", ".b4r")
		If File.Exists(ProjectFile & extension, "") Then
			ProjectFile = ProjectFile & extension
			Exit
		End If
	Next
	Log($"Project file: ${ProjectFile}"$)
	Wait For (DownloadLibrariesIndex) Complete (Unused As Boolean)
	
	HandleLayouts = GetSystemProperty("HandleLayouts", "True") = "True"
	CompactJson = GetSystemProperty("CompactJson", "True") = "True"
	RemoveApiKeys = GetSystemProperty("RemoveApiKeys", "True") = "True"
	ParseMainFile
	ProcessFiles
	Dim target As String = File.Combine(File.DirApp, $"${ProjectName}.json"$)
	File.WriteString(target, "", ConvertToJson)
	Log("Complete: " & target)
	If GetSystemProperty("DontShowExplorer", "False") = "False" Then
		Dim shl As Shell
		shl.Initialize("", "explorer.exe", Array("/select", ",", target))
		shl.RunSynchronous(-1)
	End If
	StopMessageLoop
End Sub

Private Sub ConvertToJson As String
	Dim data As Map = CreateMap("description": $"this is a summary of a B4X project.
It contains the code modules as raw code, the code modules public API, the layout files and other meta information.
It was generated by B4X CodeBundle tool v${Version}"$, _
		"platform": Platform, "project_name": ProjectName, "useful_links": _
			CreateMap("b4x_repository": "https://github.com/AnywhereSoftware/B4X_Forum_Resources/tree/main", _
				"b4x_forum": "https://www.b4x.com/android/forum/"))
	Dim LibJsons As List = B4XCollections.CreateList(Null)
	For Each lib As String In Libraries
		Dim m As Map = LibrariesIndex.GetDefault(lib, CreateMap("name": lib))
		LibJsons.Add(m)
	Next
	data.Put("libraries", LibJsons)
	If AppType <> "" Then data.Put("b4j_app_type", AppType)
	Dim CodeModules As List = B4XCollections.CreateList(Null)
	CodeModules.Add(CodeModuleToMap(MainModule))
	For Each module In Modules
		CodeModules.Add(CodeModuleToMap(module))
	Next
	data.Put("code_modules", CodeModules)
	data.Put("project_files", Files)
	If Platform = "b4a" Then
		Dim ManifestCode As String = MainModule.Headers.Get("ManifestCode")
		ManifestCode = ManifestCode.Replace("~\n~", CRLF)
		If RemoveApiKeys Then
			ManifestCode = SanitizeCode(ManifestCode)
		End If
		data.Put("android_manifest", ManifestCode)
	End If
	Return IIf(CompactJson, data.As(JSON).ToCompactString, data.As(JSON).ToString)
End Sub

Private Sub ProcessFiles
	Dim bal As BalConverter
	bal.Initialize(False)
	Files.Initialize
	Dim FilesFolder As String = File.Combine(ProjectDir, "Files")
	For Each f As String In File.ListFiles(FilesFolder)
		Dim m As Map = CreateMap("name": f)
		Files.Add(m)
		If Regex.IsMatch("b\wl", GetExtension(f)) Then
			If HandleLayouts Then
				m.Put("layout", bal.ConvertBalToJsonInMemory(FilesFolder, f))
			End If
		End If
	Next
End Sub

Private Sub GetExtension(f As String) As String
	Dim i As Int = f.LastIndexOf(".")
	If i = -1 Then Return ""
	Return f.SubString(i + 1).ToLowerCase
End Sub

Private Sub CodeModuleToMap (cm As CodeModule) As Map
	Dim m As Map = CreateMap("name": cm.Name, "module_type": cm.ModuleType, "code": cm.Code)
	Dim PublicAPI As Map = CreateMap("events": cm.Clazz.Events.AsList)
	PutIfNotEmpty(PublicAPI, "module_comment", cm.Clazz.Comment)
	Dim c As Map = CreateMap()
	For Each t As String In Array("method", "field", "property")
		Dim members As List = AddMembers(cm.Clazz.Members, t)
		If members.Size > 0 Then c.Put(t, members)
	Next
	m.put("module_public_elements", c)
	Return m
End Sub

Private Sub AddMembers (members As Map, MemberType As String) As List
	Dim res As List
	res.Initialize
	For Each Member As Member In members.Values
		If Member.MemberType <> MemberType Then Continue
		Dim m As Map = CreateMap("name": Member.Name, "comment": Member.Comment, "returntype": Member.ReturnType)
		If Member.Parameters.IsInitialized And Member.Parameters.Size > 0 Then
			Dim parameters As List
			parameters.Initialize
			For Each param As Parameter In Member.Parameters
				parameters.Add(CreateMap("name": param.Name, "type": param.VarType))
			Next
			m.Put("parameter", parameters)
		End If
		res.Add(m)
	Next
	Return res
End Sub

Private Sub PutIfNotEmpty(m As Map, key As String, value As String)
	value = value.Trim
	If value <> "" Then m.Put(key, value)
End Sub


Private Sub ParseMainFile
	Libraries.Initialize
	Modules.Initialize
	Platform = File.GetName(ProjectFile).ToLowerCase
	Platform = Platform.SubString(Platform.LastIndexOf(".") + 1)
	ProjectDir = File.GetFileParent(ProjectFile)
	MainModule = ReadFile(ProjectFile)
	ProjectName = MainModule.Name
	MainModule.Name = "Main"
	For Each key As String In MainModule.Headers.Keys
		If key.StartsWith("Library") Then
			Libraries.Add(MainModule.Headers.Get(key))
		Else If key.StartsWith("Module") Then
			Modules.Add(ReadModule(MainModule.Headers.Get(key)))
		End If
	Next
	If Platform = "b4j" Then
		AppType = MainModule.Headers.Get("AppType")
	End If
	MainModule.Clazz = ParseModule(MainModule)
End Sub

Private Sub ReadModule(ModulePath As String) As CodeModule
	If ModulePath.StartsWith("|relative|") Then
		ModulePath = File.Combine(ProjectDir, ModulePath.SubString("|relative|".Length))
	Else If ModulePath.StartsWith("|absolute|") Then
		ModulePath = ModulePath.SubString("|absolute|".Length)
	Else
		ModulePath = File.Combine(ProjectDir, ModulePath)
	End If
	Dim cm As CodeModule = ReadFile(ModulePath & ".bas")
	cm.Clazz = ParseModule(cm)
	Return cm
End Sub

Private Sub ReadFile(Path As String) As CodeModule
	Dim cm As CodeModule
	cm.Initialize
	cm.Headers.Initialize
	Dim text As String = File.ReadString(Path, "")
	If text.StartsWith(Chr(0xFEFF)) Then
		text = text.SubString(1)
	End If
	Dim i As Int = text.IndexOf("@EndOfDesignText@")
	Dim headers As String = text.SubString2(0, i)
	Dim m As Matcher = Regex.Matcher2("^([\w\d_]+)=(.*)$", Regex.MULTILINE, headers)
	Do While m.Find
		Dim key As String = m.Group(1)
		Dim value As String = m.Group(2)
		cm.Headers.Put(key, value)
	Loop
	i = text.IndexOf2(CRLF, i + 1)
	cm.Code = text.SubString(i + 2)
	If RemoveApiKeys Then
		cm.Code = SanitizeCode(cm.Code)
	End If
	cm.Name = File.GetName(Path)
	cm.Name = cm.Name.SubString2(0, cm.Name.LastIndexOf("."))
	cm.ModuleType = cm.Headers.GetDefault("Type", "")
	Return cm
End Sub

Private Sub SanitizeCode (Code As String) As String
	Dim sb As StringBuilder
	sb.Initialize
	Dim lastMatchEnd As Int = 0
	Dim m As Matcher = Regex.Matcher($"(\"|<string>)([A-Za-z0-9_\-:~/]{24,})(\"|</string>)"$, Code)
	Do While m.Find
		Dim currentStart As Int = m.GetStart(0)
		sb.Append(Code.SubString2(lastMatchEnd, currentStart))
		lastMatchEnd = m.GetEnd(0)
		Dim key As String = m.Group(0)
		If IsApiKey(key) Then
			Log("Key removed: " & key)
			sb.Append(m.Group(1)).Append($"0000-0000"$).Append(m.Group(3))
		Else
			sb.Append(key)
		End If
	Loop
	If lastMatchEnd < Code.Length Then sb.Append(Code.SubString(lastMatchEnd))
	Return sb.ToString
End Sub

Private Sub IsApiKey (Key As String) As Boolean
	If Key.EndsWith(":""") Then Return False
	If Regex.IsMatch("(\D*\d){2,}.*", Key) = False Then Return False
	Return True
End Sub



Private Sub ParseModule (cm As CodeModule) As Class
	Dim cls As Class
	cls.Initialize
	cls.Members.Initialize
	cls.Events.Initialize
	For Each line As String In Regex.Split("\r\n", cm.Code)
		Dim m As Matcher = Regex.Matcher("^\s*'(.*)", line)
		If m.Find Then
			CurrentComment = CurrentComment & CRLF & m.Group(1).Trim
		Else If Regex.IsMatch2("^\s*public\s*sub\s+.*", Regex.CASE_INSENSITIVE, line) Then
			Dim member As Member = ParseSubLine(line)
			If member <> Null Then
				If cls.Members.ContainsKey(member.Name.ToLowerCase) = False Then
					cls.Members.Put(member.Name.ToLowerCase, member)
				Else if member.MemberType = "property" Then
					Dim ExistingProperty As Member = cls.Members.Get(member.Name.ToLowerCase)
					If ExistingProperty.MemberType = "property" Then
						If member.ReturnType <> "" Then ExistingProperty.ReturnType = member.ReturnType
						ExistingProperty.Parameters.AddAll(member.Parameters)
					End If
					
				End If
			End If
			CurrentComment = ""
		Else
			m = Regex.Matcher2("^\s*#Event:\s*(.*)$", Regex.CASE_INSENSITIVE, line)
			If m.Find Then
				cls.Events.Add(m.Group(1))
			Else
				m = Regex.Matcher2("^\s*public\s+([\w_]+)\s+as\s+([\w_]+)", Regex.CASE_INSENSITIVE, line)
				If m.Find Then
					Dim field As Member
					field.Initialize
					field.MemberType = "field"
					field.Name = m.Group(1)
					field.ReturnType = m.Group(2)
					field.Comment = CurrentComment
					cls.Members.Put(field.Name.ToLowerCase, field)
				End If
			End If
			
			CurrentComment = ""
		End If
	Next
	Return cls
End Sub

Private Sub ParseSubLine (Line As String) As Member
	Dim m As Matcher = Regex.Matcher2("^\s*public\s*sub\s+([^\s(]+)", Regex.CASE_INSENSITIVE, Line)
	If m.Find = False Then Return Null
	Dim member As Member
	member.Initialize
	member.MemberType = "method"
	member.Name = m.Group(1)
	If member.Name.StartsWith("get") Or member.Name.StartsWith("set") Then
		member.MemberType = "property"
		member.Name = member.Name.SubString(3)
	End If
	member.Parameters.Initialize
	member.Comment = CurrentComment.Trim
	Dim i1 As Int = Line.IndexOf("(")
	Dim i2 As Int = Line.LastIndexOf(")")
	If i1 > -1 And i2 > -1 Then
		m = Regex.Matcher2("([\w_]+)(?:\(\))?\s+as\s+([\w_]+)", Regex.CASE_INSENSITIVE, Line.SubString2(i1 + 1, i2))
		Do While m.Find
			Dim p As Parameter
			p.Initialize
			p.Name = m.Group(1)
			p.VarType = m.Group(2)
			member.Parameters.Add(p)
		Loop
		m = Regex.Matcher2("as\s+([\w_]+(?:\(\))?)$", Regex.CASE_INSENSITIVE, Line)
		If m.Find Then
			member.ReturnType = m.Group(1)
		End If
	Else
		m = Regex.Matcher2("as\s+([\w_]+(?:\(\))?)$", Regex.CASE_INSENSITIVE, Line)
		If m.Find Then
			member.ReturnType = m.Group(1)
		End If
	End If
	Return member
End Sub
