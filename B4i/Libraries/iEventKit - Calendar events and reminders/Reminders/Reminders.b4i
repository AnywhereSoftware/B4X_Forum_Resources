Build1=Default,b4i.example2
File1=1.bil
FileGroup1=Default Group
Group=Default Group
Library1=icore
Library2=iui8
Library3=ieventkit
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=0
Version=8.51
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: B4i Example
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#ATSEnabled: True
	#MinVersion: 7
#End Region

#MinVersion: 12
'iOS 16-
#PlistExtra:<key>NSRemindersUsageDescription</key><string>description here...</string>
'iOS 17+
#PlistExtra:<key>NSRemindersFullAccessUsageDescription</key><string>description here...</string>
Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page
	Private store As EventStore
	Private btnCreateReminder As Button
	Private btnList As Button
	Private btnAccess As Button
	Private TableView1 As TableView
	
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
	Page1.Initialize("Page1")
	Page1.RootPanel.LoadLayout("1")
	NavControl.ShowPage(Page1)
	store.Initialize("store")
	If store.IsAuthorized(store.AuthorizationStatusReminders) = False Then
		'not authorized
		btnList.Enabled = False
		btnCreateReminder.Enabled = False
		If store.AuthorizationStatusReminders = store.AUTHORIZATION_NOT_DETERMINED Then
			btnAccess.Visible = True
		Else
			'the user already denied access in the past. It is not possible to show the dialog again.
			Store_AuthorizationGrantedReminders (False)
		End If
	End If
End Sub

Sub btnAccess_Click
	store.RequestAccessReminders
End Sub
Sub Store_AuthorizationGrantedReminders (Success As Boolean)
	btnAccess.Visible = False
	If Success = False Then
		Msgbox("Access denied. Please enable it under Settings", "")
	Else
		btnList.Enabled = True
		btnCreateReminder.Enabled = True
	End If
End Sub

Sub btnList_Click
	TableView1.Clear
	store.FindReminders(0, 0, False) 'find all uncompleted
End Sub

Sub Store_RemindersReady (Success As Boolean, Reminders As List)
	If Success Then
		For Each r As Reminder In Reminders
			TableView1.AddSingleLine(r.Title)
		Next
		TableView1.ReloadAll
	End If
End Sub

Sub btnCreateReminder_Click
	Dim r As Reminder = store.CreateReminder
	r.Title = "New reminder"
	r.Completed = False
	r.SetDueDate(DateTime.Now + 1 * DateTime.TicksPerMinute, False)
	store.SaveReminder(r)
	btnList_Click
End Sub



Private Sub Page1_Resize(Width As Int, Height As Int)
	
End Sub

Private Sub Application_Background
	
End Sub


