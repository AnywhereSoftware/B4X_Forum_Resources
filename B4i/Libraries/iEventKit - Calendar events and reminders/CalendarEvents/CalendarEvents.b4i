Build1=Default,b4i.example21
File1=1.bil
FileGroup1=Default Group
Group=Default Group
Library1=icore
Library2=ieventkit
Library3=iui8
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=0
Version=8.51
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: B4i Example
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#ATSEnabled: True
	#MinVersion: 7
#End Region
#MinVersion: 12
'iOS 17+
#PlistExtra:<key>NSCalendarsFullAccessUsageDescription</key><string>description here...</string>
'iOS 16-
#PlistExtra:<key>NSCalendarsUsageDescription</key><string>description here...</string>
Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page
	Private store As EventStore
	Private btnCreateEvent As Button
	Private btnList As Button
	Private btnAccess As Button
	Private TableView1 As TableView
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
	Page1.Initialize("Page1")
	Page1.RootPanel.LoadLayout("1")
	NavControl.ShowPage(Page1)
	store.Initialize("store")
	If store.IsAuthorized(store.AuthorizationStatusEvents) = False Then
		'not authorized
		btnList.Enabled = False
		btnCreateEvent.Enabled = False
		If store.AuthorizationStatusEvents = store.AUTHORIZATION_NOT_DETERMINED Then
			btnAccess.Visible = True
		Else
			'the user already denied access in the past. It is not possible to show the dialog again.
			store_AuthorizationGrantedEvents (False)
		End If
	End If
End Sub

Sub btnAccess_Click
	store.RequestAccessEvents
End Sub

Sub store_AuthorizationGrantedEvents (Success As Boolean)
	Log(LastException)
	btnAccess.Visible = False
	If Success = False Then
		Msgbox("Access denied. Please enable it under Settings", "")
	Else
		btnList.Enabled = True
		btnCreateEvent.Enabled = True
	End If
End Sub

Sub btnList_Click
	TableView1.Clear
	store.FindEvents(DateTime.Now - 14 * DateTime.TicksPerDay, DateTime.Now + 14 * DateTime.TicksPerDay)
End Sub

Sub store_EventsReady (Success As Boolean, Events As List)
	If Success Then
		For Each e As CalendarEvent In Events
			TableView1.AddTwoLines(e.Title, $"$DateTime{e.StartDate} - $DateTime{e.EndDate}"$)
		Next
		TableView1.ReloadAll
	End If

End Sub

Sub btnCreateEvent_Click
	Dim ev As CalendarEvent = store.CreateEvent
	ev.Title = "New Event"
	ev.StartDate = DateTime.Now + 1 * DateTime.TicksPerMinute
	ev.EndDate = ev.StartDate + 30 * DateTime.TicksPerMinute
	Dim alarm As CalendarAlarm
	alarm.InitializeRelative(0.5) '1/2 minute before the start date.
	ev.AddAlarm(alarm)
	store.SaveEvent(ev, False)
	btnList_Click
End Sub

Private Sub Page1_Resize(Width As Int, Height As Int)
	
End Sub

Private Sub Application_Background
	
End Sub




