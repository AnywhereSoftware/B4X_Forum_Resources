Version=4.4
NumberOfModules=0
Build1=Default,b4i.game
NumberOfFiles=1
File1=1.bil
NumberOfLibraries=3
Library1=icore
Library2=igamecenter
Library3=icustomlistview
@EndOfDesignText@
 'Code module
#Region  Project Attributes 
	#ApplicationLabel: B4i Game
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#ATSEnabled: True
	#MinVersion: 7
#End Region
#ProvisionFile: game.mobileprovision
#CertificateFile: ios_development.cer
#DeviceCapabilities: gamekit
Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page
	Private Game As GKGameCenter
	Private btnSignIn As Button
	Private btnSubmit As Button
	Private txtScore As TextField
	Private lblAlias As Label
	Private lblUser As Label
	Private CustomListView1 As CustomListView
	Private btnRefresh As Button
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
	Page1.Initialize("Page1")
	Page1.RootPanel.LoadLayout("1")
	NavControl.ShowPage(Page1)
	Game.Initialize("GameCenter")
	btnSignIn.Enabled = False
End Sub

Sub GameCenter_AuthenticationStateChanged (Authenticated As Boolean, ShouldShowDialog As Boolean)
	btnSignIn.Enabled = False
	If Authenticated Then
		Log("Authenticated!")
		lblUser.Text = "Display Name: " & Game.LocalPlayer.DisplayName
		lblAlias.Text = "Alias: " & Game.LocalPlayer.Alias
		btnRefresh_Click
	Else
		lblUser.Text = "Display Name: N/A"
		lblAlias.Text = "Alias: N/A"
		If ShouldShowDialog Then
			btnSignIn.Enabled = True
		Else
			Msgbox("Error: " & LastException, "")
		End If
	End If
	btnRefresh.Enabled = Authenticated
	btnSubmit.Enabled = Authenticated
End Sub

Sub btnSignIn_Click
	Game.ShowDialog(Page1)
End Sub

Sub btnSubmit_Click
	Page1.ResignFocus
	If IsNumber(txtScore.Text) Then
		Dim s As GKScore
		s.Initialize("Board1")
		s.Value = txtScore.Text
		Wait For (Game.SubmitScore(s)) GameCenter_ScoreSubmitted (Success As Boolean)
		Log($"Score submitted: ${Success}"$)
	End If
End Sub

Sub btnRefresh_Click
	If Game.LocalPlayer.Authenticated Then
		Dim sf As Object = Game.RequestScores("Board1")
		Wait For (sf) GameCenter_ScoresAvailable (Success As Boolean, Scores As List, PlayerScore As GKScore)
		If Success = False Then
			Log(LastException)
		Else
			CustomListView1.Clear
			For Each score As GKScore In Scores
				CustomListView1.AddTextItem($"Player: ${score.Player.Alias}
Score: ${score.Value}
Date: $DateTime{score.Date}"$, "")
			Next
		End If
	End If
End Sub


Private Sub Page1_Resize(Width As Int, Height As Int)
	
End Sub

Private Sub Application_Background
	
End Sub





