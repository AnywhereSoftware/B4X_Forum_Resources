Build1=Default,b4i.example2
File1=1.bil
File2=2.bil
File3=page1layout.bil
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=icore
Library2=xcustomlistview
Library3=xui views
NumberOfFiles=3
NumberOfLibraries=3
NumberOfModules=0
Version=6.3
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: B4i Example
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#ATSEnabled: True
	#MinVersion: 8
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page

	Private lblMexDest As Label
	Private lblOrarioMexDest As Label
	Private pnlBoxMexDest As Panel
	Private lblMexMit As Label
	Private lblOrarioMexMit As Label
	Private pnlBoxMexMit As Panel
	
	Private CustomListView1 As CustomListView
	
	Private btnSend As Button
	Private TextView1 As TextView
	
	Private widthtxt As Int
	Private textlenght As Int = 0
	
	Type mex (Name As String, Text As String)
End Sub

Private Sub Application_Start (Nav As NavigationController)
	'SetDebugAutoFlushLogs(True) 'Uncomment if program crashes before all logs are printed.
	NavControl = Nav
	Page1.Initialize("Chat")
	Page1.Title = "Chat"
	Page1.RootPanel.LoadLayout("page1layout")
	NavControl.ShowPage(Page1)
	
	SendMex("Bot", "Hi")
End Sub

Private Sub Application_Background
	
End Sub

Sub Chat_Resize (Width As Float, Height As Float)
	'Set the layout programmatically (Handle Resize event in layout is disabled)
	 
	TextView1.Text = ""
	TextView1.SizeToFit
		
	TextView1.Top = 100%y - TextView1.Height -10dip
	TextView1.Left	= 15dip
	TextView1.Width = 85%x
	
	btnSend.Height = TextView1.Height
	btnSend.Left = TextView1.Width
	btnSend.Width = 100%x - TextView1.Width
	btnSend.Top = TextView1.Top
	
	CustomListView1.AsView.Width = 100%x
	CustomListView1.AsView.Left = 0
	CustomListView1.AsView.Top = 0
	CustomListView1.AsView.Height = TextView1.Top - 10dip
	
	widthtxt = TextView1.Width 'Set a fixed width, used in the code always after the .SizeToFit in order to change only the height
End Sub

Sub CustomListView1_VisibleRangeChanged (FirstIndex As Int, LastIndex As Int)
	Dim ExtraSize As Int = 2
	For i = 0 To CustomListView1.Size - 1
		Dim p As Panel = CustomListView1.GetPanel(i)
		If (i > (FirstIndex - ExtraSize)) And (i < (LastIndex + ExtraSize)) Then
			If p.NumberOfViews = 0 Then
				Dim m As mex = CustomListView1.GetValue(i)
				
				If m.Name == "Bot" Then
					p.LoadLayout("1")
					lblMexDest.Text = m.Text
					lblMexDest.Font = Font.CreateNew(15)
					lblMexDest.SizeToFit
					lblMexDest.Top = 5dip
					lblMexDest.Left = 5dip
					
					If lblMexDest.Width > p.Width - 100dip Then
						lblMexDest.Width = p.Width - 100dip
						lblMexDest.SizeToFit
					End If
					
					DateTime.TimeFormat = "HH:mm"
					lblOrarioMexDest.Text = DateTime.Time(DateTime.Now)
					lblOrarioMexDest.Font = Font.CreateNew(10)
					lblOrarioMexDest.SizeToFit
					
					lblOrarioMexDest.Left = (lblMexDest.Left + lblMexDest.Width) + 2.5dip 'lblMexDest.Right +2.5dip
					lblOrarioMexDest.Top = (lblMexDest.Top + lblMexDest.Height) 'lblMexDest.Bottom
					'
					pnlBoxMexDest.Height = lblMexDest.Height + lblOrarioMexDest.Height + 10dip
					pnlBoxMexDest.Width = (lblOrarioMexDest.Left + lblOrarioMexDest.Width) + 5dip 'lblOrarioMexDest.Right + 5dip
					pnlBoxMexDest.Top = 0
					pnlBoxMexDest.Left = 20dip
					
					CustomListView1.ResizeItem(i, pnlBoxMexDest.Height)
				Else
					p.LoadLayout("2")
					lblMexMit.Text = m.Text
					lblMexMit.Font = Font.CreateNew(15)
					lblMexMit.SizeToFit
					lblMexMit.Top = 5dip
					lblMexMit.Left = 5dip
					
					If lblMexMit.Width > p.Width - 100dip Then
						lblMexMit.Width = p.Width - 100dip
						lblMexMit.SizeToFit
					End If
					
					DateTime.TimeFormat = "HH:mm"
					lblOrarioMexMit.Text = DateTime.Time(DateTime.Now)
					lblOrarioMexMit.Font = Font.CreateNew(10)
					lblOrarioMexMit.SizeToFit
					
					lblOrarioMexMit.Left = (lblMexMit.Left + lblMexMit.Width) + 2.5dip 'lblMexMit.Right +2.5dip
					lblOrarioMexMit.Top = (lblMexMit.Top + lblMexMit.Height) 'lblMexMit.Bottom
	
					pnlBoxMexMit.Height = lblMexMit.Height + lblOrarioMexMit.Height + 10dip
					pnlBoxMexMit.Width = (lblOrarioMexMit.Left + lblOrarioMexMit.Width) + 5dip 'lblOrarioMexMit.Right + 5dip
					pnlBoxMexMit.Top = 0
					pnlBoxMexMit.Left = p.Width - pnlBoxMexMit.Width - 20dip
					
					CustomListView1.ResizeItem(i, pnlBoxMexMit.Height)
				End If
			End If
		Else
			If p.NumberOfViews > 0 Then
				p.RemoveAllViews
			End If
		End If
	Next
End Sub

Sub TextView1_TextChanged (OldText As String, NewText As String)
	If  TextView1.Height < 30%y Then 'Adjust the height while writing (if the height isnt' greater then 30%y)
		TextView1.SizeToFit
		TextView1.Width = widthtxt
	Else If textlenght == 0 Then
		textlenght = TextView1.Text.Length
	End If
	
	If (OldText.Length > NewText.Length) And (TextView1.Text.Length < textlenght) Then 'Adjust the height when deleting text
		TextView1.SizeToFit
		TextView1.Width = widthtxt
	End If
	
	TextView1.Top = (btnSend.Top + btnSend.Height) - TextView1.Height
	
	If NewText == "" Then
		btnSend.Enabled = False
	Else
		btnSend.Enabled = True	
	End If
	
	CustomListView1.AsView.Top = 0
	CustomListView1.AsView.Height = TextView1.Top - 10dip
End Sub

Sub Chat_KeyboardStateChanged (height As Float)
	Dim v As View = CustomListView1.AsView
	Dim h As Double = v.CalcRelativeKeyboardHeight(height)
	
	TextView1.Top = Min(h, Page1.RootPanel.Height - v.Top) - TextView1.Height -10dip
	btnSend.Top = TextView1.top
	
	CustomListView1.AsView.Top = 0
	CustomListView1.AsView.Height = TextView1.Top - 10dip
End Sub

Sub btnSend_Click
	SendMex("Me", TextView1.Text)
	TextView1.Text = ""
	btnSend.Enabled = False
	
	BotResponse
End Sub

Sub SendMex(Name As String, Text As String)
	Dim p As Panel
	p.Initialize("p")
	p.Top = 0
	p.Left = 0
	p.Color = Colors.Transparent
	
	Dim m As mex
	m.Name = Name
	m.Text = Text
		
	CustomListView1.Add(p, m)
End Sub

Sub BotResponse
	If CustomListView1.Size = 2 Then
		Sleep(1200)
		SendMex("Bot", "How are you?")
	else if CustomListView1.Size = 4 Then
		Sleep(1200)
		SendMex("Bot", "Something new? 🤔")
	else if CustomListView1.Size = 6 Then
		Sleep(1200)
		SendMex("Bot", "Let's buy some 🍺🍺🍺🍻🍻")
	else if CustomListView1.Size = 8 Then
		Sleep(1200)
		SendMex("Bot", "Yeah!")
	End If
End Sub