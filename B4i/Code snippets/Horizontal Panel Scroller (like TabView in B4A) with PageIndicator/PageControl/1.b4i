Build1=Default,b4i.example2
File1=1.bil
File2=centering.bil
FileGroup1=Default Group
FileGroup2=Default Group
Group=Default Group
Library1=icore
NumberOfFiles=2
NumberOfLibraries=1
NumberOfModules=0
Version=6
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: Horizontal Panel Scroller
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#MinVersion: 7
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page

	Private ScrollView1 As ScrollView
	Private panels As List
	Private PageControl As View
	Private noPageControl As NativeObject
	
	Private Auto As Boolean
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
	Page1.Initialize("Page1")
	Page1.Title = "Page 1"
	Page1.RootPanel.LoadLayout("1")
	NavControl.ShowPage(Page1)
	panels.Initialize
	
	ScrollView1.ContentHeight = ScrollView1.Height
	ScrollView1.ContentWidth = ScrollView1.Width
	
	Auto = True 'Set to True to enable automatic scroll example
	
	For i = 0 To 4
		Dim p As Panel
		p.Initialize("p")
		p.Tag = i
		p.LoadLayout("centering")
		p.Color = Rnd(0x7FFFFFF, -1)
		panels.Add(p)
		ScrollView1.Panel.AddView(p, 0, 0, ScrollView1.Width, ScrollView1.Height)
	Next
		
	noPageControl = noPageControl.Initialize("UIPageControl").RunMethod("new", Null)
	PageControl = noPageControl
	Page1.RootPanel.AddView(PageControl, 0, 80%y, 100%x, 40dip)
	noPageControl.SetField("numberOfPages", panels.Size)
	noPageControl.SetField("enabled", False)
	noPageControl.SetField("pageIndicatorTintColor", noPageControl.ColorToUIColor(Colors.LightGray))
	noPageControl.SetField("currentPageIndicatorTintColor", noPageControl.ColorToUIColor(0xFF00AEFF))
	'Page1.RootPanel.AddView(PageControl, -50%x, 80%y, 100%x, 40dip)
	
	If Auto Then
		ScrollView1.UserInteractionEnabled = False
		For i=0 To panels.Size-1
			ScrollView1.ScrollTo(i*ScrollView1.Width, 0, True) ' "i" is the number of the page to go to
			Sleep(1000)
		Next
		ScrollView1.UserInteractionEnabled = True
	End If
End Sub

Private Sub Page1_Resize(Width As Int, Height As Int)
	ScrollView1.ContentHeight = ScrollView1.Height
	ScrollView1.ContentWidth = ScrollView1.Width * panels.Size
	For i = 0 To panels.Size - 1
		Dim p As Panel = panels.Get(i)
		p.SetLayoutAnimated(0, 1, i * ScrollView1.Width, 0, ScrollView1.Width, ScrollView1.Height)
	Next
	PageControl.SetLayoutAnimated(0, 0.5, 0, ScrollView1.Top + ScrollView1.Height + 10dip, 100%x, 40dip)
End Sub

Private Sub Application_Background
	
End Sub


Sub ScrollView1_ScrollChanged (OffsetX As Int, OffsetY As Int)
	For Each p As Panel In panels
		If p.Left = OffsetX Then
			noPageControl.SetField("currentPage", p.Tag)
		End If
	Next
End Sub