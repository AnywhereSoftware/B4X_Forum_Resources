Build1=Default,mobi.mindware.tableexample,default
File1=1.bil
File2=hamburger.png
File3=left.bil
File4=logo-02.png
File5=splash.bil
File6=titlenav.bil
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
Group=Default Group
Library1=icore
Library2=istringutils
Library3=xui views
Module1=ActivityClass
Module2=|relative|..\b4a_drawer_starter\B4XDrawer
Module3=|relative|..\b4a_drawer_starter\Main2
Module4=Map_B4X
Module5=|relative|..\b4a_drawer_starter\Starter
NumberOfFiles=6
NumberOfLibraries=3
NumberOfModules=5
Version=6.28
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: Cross Platform Example
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#MinVersion: 8
	#CertificateFile: ios_development.cer
	#ProvisionFile: b4i_development.mobileprovision	
#End Region

'*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
'Starter iOS app
'by Jack Cole, Mindware Consulting, Inc.
'see: https://www.b4x.com/android/forum/forums/ios-tutorials.63/create-thread
'*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

Sub Process_Globals
	Public App As Application
	Public NavControl As NavigationController
	Public Page1 As Page
	'used with android activity modules
	'keeps a map of activity class / pages for the nav controller
	Public ActivityPageMap As Map_B4X
	Public ActivitiesOpenedDuringSession As List ' list of activities that have been opened during the
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
'	NavControl.NavigationBarVisible=False
	Starter.Service_Create
	
	ActivityPageMap.Initialize
	Page1.Initialize("Page1")
	Page1.RootPanel.LoadLayout("splash")
	NavControl.ShowPage(Page1)
	Sleep(2000)	
	StartActivity(Main2,"Main2")
End Sub


Private Sub Page1_Resize(Width As Int, Height As Int)
	
End Sub

Private Sub Application_Background
	PauseCurrentActivity(False)
End Sub

Public Sub Application_Foreground
	Dim CurrentActivity As ActivityClass = ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
	CurrentActivity.Activity_Resume
End Sub

Public Sub StartActivity(Activity As Object,Event As String)
	PauseCurrentActivity(False)
	Dim ActivityClassHelper As ActivityClass
	ActivityClassHelper.Initialize(Activity,Event)
	ActivityPageMap.Put(Event, ActivityClassHelper)
End Sub

Sub PauseCurrentActivity(UserClosed)
	If ActivityPageMap.Size>0 Then
		Dim CurrentActivity As ActivityClass = ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
		CurrentActivity.Activity_Pause(UserClosed)
	End If
End Sub

Sub GoBack(Event As String)
'	Log("going back "&Event)
	PauseCurrentActivity(True)
	If ActivityPageMap.ContainsKey(Event) Then
		ActivityPageMap.Remove(Event)
	End If
	
	NavControl.RemoveCurrentPage
	If ActivityPageMap.ToMap.Size=0 Then
		StartActivity(Main2,"Main2")
	Else
		Dim AC As ActivityClass=ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
		AC.Activity_Resume
	End If
End Sub

'Mimics Behavior of ExitApplication in b4a when called from another activity
Sub ExitApplication
	Dim l As List
	l.Initialize
	l.Add(Page1)
	NavControl.SetPagesStack(l)
	Application_Start(NavControl)
End Sub