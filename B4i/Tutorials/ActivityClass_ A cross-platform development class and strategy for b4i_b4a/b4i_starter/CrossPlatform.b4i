Build1=Default,mobi.mindware.tableexample,default
File1=main2_menu.bil
File2=red-yin-yang-hi.png
File3=splash.bil
File4=titlenav.bil
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
Group=Default Group
Library1=icore
Library2=istringutils
Library3=xui views
Module1=ActivityClass
Module2=|relative|..\b4a_starter\Main2
Module3=Map_B4X
Module4=|relative|..\b4a_starter\Starter
NumberOfFiles=4
NumberOfLibraries=3
NumberOfModules=4
Version=6.5
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: Cross Platform Example
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
	#Target: iPhone, iPad
	#MinVersion: 8
#End Region

'*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
'Starter iOS app
'by Jack Cole, Mindware Consulting, Inc.
'see: https://www.b4x.com/android/forum/forums/ios-tutorials.63/create-thread
'*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

Sub Process_Globals
	Public App As Application
	Public NavControl As NavigationController
	Public Page1 As Page
	'used with android activity modules
	'keeps a map of activity class / pages for the nav controller
	Public ActivityPageMap As Map_B4X
	Public ActivitiesOpenedDuringSession As List ' list of activities that have been opened during the
End Sub

Private Sub Application_Start (Nav As NavigationController)
	NavControl = Nav
	NavControl.NavigationBarVisible=False
	Starter.Service_Create
	
	ActivityPageMap.Initialize
	Page1.Initialize("Page1")
	Page1.RootPanel.LoadLayout("splash")
	NavControl.ShowPage(Page1)
	Sleep(2000)	
	StartActivity(Main2,"Main2")
End Sub


Private Sub Page1_Resize(Width As Int, Height As Int)
	
End Sub

Private Sub Application_Background
	PauseCurrentActivity(False)
End Sub

Public Sub Application_Foreground
	Dim CurrentActivity As ActivityClass = ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
	CurrentActivity.Activity_Resume
End Sub

Public Sub StartActivity(Activity As Object,Event As String)
	'LogColor("StartActivity: "&Event, Colors.Green)
	If ActivityPageMap.Size>0 Then
		Dim CurrentActivity As ActivityClass = ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
		'exit if the current activity is the one requested to start
		If CurrentActivity.ActivityEvent=Event Then Return
		PauseCurrentActivity(False)
	End If
	
	Dim ActivityClassHelper As ActivityClass
	ActivityClassHelper.Initialize(Activity,Event)
	ActivityPageMap.Put(Event, ActivityClassHelper)
End Sub

Sub PauseCurrentActivity(UserClosed)
	If ActivityPageMap.Size>0 Then
		Dim CurrentActivity As ActivityClass = ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
		CurrentActivity.Activity_Pause(UserClosed)
	End If
End Sub

Sub GoBack(Event As String)
'	Log("going back "&Event)
	PauseCurrentActivity(True)
	If ActivityPageMap.ContainsKey(Event) Then
		ActivityPageMap.Remove(Event)
	End If
	
	NavControl.RemoveCurrentPage
	Sleep(200)
	
	If ActivityPageMap.ToMap.Size=0 Then
		StartActivity(Main2,"Main2")
	Else
		Dim AC As ActivityClass=ActivityPageMap.Values.Get(ActivityPageMap.Values.Size-1)
		AC.Activity_Resume
	End If
End Sub

Sub ExitApplication
	Dim l As List
	l.Initialize
	l.Add(Page1)
	Page1.RootPanel.RemoveAllViews
	Dim blankList As List
	ActivitiesOpenedDuringSession=blankList
	Dim blankMap As Map_B4X
	ActivityPageMap=blankMap
	NavControl.SetPagesStack(l)
'	Process_Globals
	Application_Start(NavControl)
End Sub