Build1=Default,b4a.example
File1=main.bal
FileGroup1=Default Group
Group=Default Group
Library1=core
Library2=jmqtt
Library3=randomaccessfile
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: https://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="5" android:targetSdkVersion="28"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~CreateResourceFromFile(Macro, Themes.DarkTheme)~\n~'End of default text.~\n~AddPermission(android.permission.INTERNET)
Module1=Starter
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=1
Version=9.9
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: AuthorizationDemo
	#VersionCode: 1
	#VersionName: 
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: portrait
	#CanInstallToExternalStorage: False
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: True
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
	'These variables can only be accessed from this module.

	Private etLog As EditText
	Private btnConnectGuest As Button
	Private btnConnectFred As Button
	Private btnConnectReader As Button
	Private btnConnectInvalidUser As Button
	Private btnConnectInvalidPwd As Button
	Private btnPublish As Button
	Private btnDisconnect As Button

	Dim Client As MqttClient
	Dim ClientID As String
	Dim SBLog As StringBuilder

	Dim Const IPAddress As String = "192.168.1.7" 'Broker IP Address
	Dim Const Port As Int = 1883
	Dim Const ServerURI As String = "tcp://" & IPAddress & ":" & Port

	Dim Serializator As B4XSerializator
	Type typData(Publisher As String, Msg As String)
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Activity.LoadLayout("main")

	etLog.InputType = etLog.INPUT_TYPE_NONE
	etLog.SingleLine = False
	etLog.Wrap = True
	SBLog.Initialize

	ClientID = "User" & Rnd(0, 100000)
End Sub

Sub Activity_Resume
End Sub

Sub AddToLog(Text As String)
	Log(Text)
	SBLog.Append(Text).Append(CRLF)
	etLog.Text = SBLog.ToString
	etLog.SelectAll 'Forces the EditText to scroll
End Sub

Sub NewConnection
	etLog.Text = ""
	AddToLog("Connection to " & ServerURI & " with ID " & ClientID)
	Client.Initialize("Monitor", ServerURI, ClientID)
End Sub

Sub btnConnectGuest_Click
	NewConnection
	AddToLog("Connecting anonymously...")
	Client.Connect
End Sub

Sub btnConnectFred_Click
	NewConnection
	AddToLog("Connecting as Fred/1234...")
	Dim MCO As MqttConnectOptions
	MCO.Initialize("Fred", "1234")
	Client.Connect2(MCO)
End Sub

Sub btnConnectReader_Click
	NewConnection
	AddToLog("Connecting as Reader/testPWD...")
	Dim MCO As MqttConnectOptions
	MCO.Initialize("Reader", "testPWD")
	Client.Connect2(MCO)
End Sub

Sub btnConnectInvalidUser_Click
	NewConnection
	AddToLog("Connecting as John/12345678...")
	Dim MCO As MqttConnectOptions
	MCO.Initialize("John", "12345678")
	Client.Connect2(MCO)
End Sub

Sub btnConnectInvalidPwd_Click
	NewConnection
	AddToLog("Connecting as Fred/12345678...")
	Dim MCO As MqttConnectOptions
	MCO.Initialize("Fred", "12345678")
	Client.Connect2(MCO)
End Sub

Sub CreatePayload(Msg As String) As Byte()
	Dim Data As typData
	Data.Initialize
	Data.Publisher = ClientID
	Data.Msg = Msg
	Return Serializator.ConvertObjectToBytes(Data)
End Sub

Sub btnPublish_Click
	AddToLog("Trying to publish to the public topic...")
	Dim Bytes() As Byte = CreatePayload("Hello public world!")
	Client.Publish2("PublicTopic", Bytes, Client.QOS_2_EXACTLY_ONCE, False)
	AddToLog("Trying to publish to the private topic...")
	Dim Bytes() As Byte = CreatePayload("Hello private world!")
	Client.Publish2("PrivateTopic", Bytes, Client.QOS_2_EXACTLY_ONCE, False)
End Sub

Sub btnDisconnect_Click
	Client.Close
End Sub

Sub Monitor_Connected (Success As Boolean)
	AddToLog("Connected=" & Success)
	If Success = True Then
		btnConnectGuest.Enabled = False
		btnConnectFred.Enabled = False
		btnConnectReader.Enabled = False
		btnConnectInvalidUser.Enabled = False
		btnConnectInvalidPwd.Enabled = False
		btnPublish.Enabled = True
		btnDisconnect.Enabled = True

		AddToLog("Subscribing to a public topic...")
		Client.Subscribe("PublicTopic", Client.QOS_2_EXACTLY_ONCE)
		AddToLog("Subscribing to a private topic...")
		Client.Subscribe("PrivateTopic", Client.QOS_2_EXACTLY_ONCE)
	End If
End Sub

Sub Monitor_MessageArrived (Topic As String, Payload() As Byte)
	Dim Data As typData = Serializator.ConvertBytesToObject(Payload)
	AddToLog("Message from " & Data.Publisher & " / " & Topic & ":" & CRLF & """" & Data.Msg & """")
End Sub

Sub Monitor_Disconnected
	AddToLog("Disconnected")
	btnConnectGuest.Enabled = True
	btnConnectFred.Enabled = True
	btnConnectReader.Enabled = True
	btnConnectInvalidUser.Enabled = True
	btnConnectInvalidPwd.Enabled = True
	btnPublish.Enabled = False
	btnDisconnect.Enabled = False
End Sub

Sub Activity_Pause (UserClosed As Boolean)
	If UserClosed Then
		AddToLog("Exiting")
		Client.Close
	End If
End Sub
