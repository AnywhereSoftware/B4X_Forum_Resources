AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=byteconverter
Library2=javaobject
Library3=jcore
Library4=jfx
Library5=jreflection
Library6=jxui
Library7=jsql
Module1=Addl_CSS
Module10=StringFormat
Module2=CSVParser
Module3=HTMLPage
Module4=HTMLPage_BR
Module5=HTMLPage_Div
Module6=HTMLPage_Static
Module7=HTMLPage_Table
Module8=HTMLPage_Utils
Module9=SQL_Utils
NumberOfFiles=1
NumberOfLibraries=7
NumberOfModules=10
Version=9.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region
'Archive Project Modules - before creating B4xLib'Archive Modules: ide://run?File=C:\Java\jdk1.8.0_271\bin\java.exe&Args=-jar&Args=C:\NoInstApps\B4j\ArchiveProjectModules\ArchiveProjectModules.jar&Args=%PROJECT%\%PROJECT_NAME%.b4j

#AdditionalJar: sqlite-jdbc-3.7.2

#If Version
	v0.1 - 	Initial release
	v0.2 - 	added data-cell class as appropriate.
			added MakeScrollable to Addl_Css	
			added DontShowClose to HTMLPage
			Fixed some inaccurate comments
			moved css required for filter hits and it is always added if filtering is requested.	
			added SyncScroll for tables.  Works best when the tables have the same number of rows.
			added Classes col-num-row and header-row for more flexibility.
			fixed a bug in the hit highlighting relating to the first row on a table without column numbers.
#End If

Sub Process_Globals
	Private MainForm As Form
	Private xui As XUI 
	Private Button1 As B4XView
	Private AppName As String = "CreateHTMLPage"
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	
	xui.SetDataFolder(AppName)
	
	Dim CP As HTMLPage
	CP.Initialize
'	CP.DontGenerateCSS
	
	'Css for the whole page
	
	Dim CSS As StringBuilder
	CSS.Initialize
	
	'##########################################################################################
	'
	'				Css for the page
	'
	'##########################################################################################

	CSS.Append($"/*	Global settings */
	
${Addl_CSS.FontSansSerif}
${Addl_CSS.MultiViews}
	
/*	Tables	*/
	#tbl1 .data-c3{width:150px;}
	#tbl1 .row-num{background-color:lightgray;}
	#tbl1 .col-num{background-color:lightgray;}
	#tbl2 tr:nth-child(even) {background-color: #e0e0e0;}
	#tbl3 {background-color:skyblue;}

/*	Div	*/
	#div2{float:left;
	margin-bottom:15px;
	text-align:center;
	width:fit-content;
	margin-right:15px;}
	#div3{border 1px solid;
	border-style:double;
	text-align:left;}
	
	
/* Other	*/
${Addl_CSS.MakeScrollable(".tbldiv1")}
${Addl_CSS.TableAlignTextCenter}
${Addl_CSS.TableHeadersBold}
	.span1{font-weight:bold;}
${Addl_CSS.PositionCloseButton("","50px","","50px","150px","75px","1.25em")}
"$)
	
	CP.Css(CSS.ToString)
	
	SyncTest

	SQLTest

	Test1(CP)
	Test2(CP)
	Test3(CP)
	Test4(CP)
	Test5(CP)
	Test6(CP)
	Test7(CP)
	Test8(CP)
	CP.ShowPage
	
End Sub

	'##########################################################################################
	'
	'				New Test From an SQL table
	'
	'##########################################################################################
Private Sub SQLTest
	
	
	'Set up table
	Dim SQL As SQL
	SQL.InitializeSQLite("", ":memory:", True)
	
	Dim Statement As String = $"CREATE TABLE data (
	Score_1 INTEGER,
	Score_2 INTEGER,
	Score_3 INTEGER,
	Score_4 REAL
	);"$
	
	SQL.ExecNonQuery(Statement)
	
	Dim L As List
	L.Initialize
	
	For i = 0 To 10
		Dim L1 As List
		L1.Initialize
		For j = 0 To 2
			L1.Add(Rnd(0,2000))
		Next
		L1.Add(Rnd(0,10000) / 2)
		L.Add(L1)
	Next

	
	SQL.BeginTransaction
	Try
	
		For i = 0 To L.Size - 1
			Dim Query As StringBuilder
			Query.Initialize
			Query.Append("INSERT INTO data VALUES(?, ?, ?, ?)")
			SQL.ExecNonQuery2(Query.ToString,L.Get(i))
		
		Next
		
		SQL.TransactionSuccessful
	Catch
		SQL.Rollback
		Log(LastException.Message)
	End Try
	
	Dim RS As ResultSet = SQL.ExecQuery("SELECT * FROM data")
	
	'Get ResultSet as List
	Dim L As List = SQL_Utils.GetResultSetAsList(RS, True)
	
	'
	'Create table
	'
	
	Dim CP As HTMLPage
	CP.Initialize
	
	Dim CSS As String = $"${Addl_CSS.FontSansSerif}
${Addl_CSS.TableHeadersBold}
${Addl_CSS.PositionCloseButton("","50px","","50px","150px","75px","1.25em")}
	#tbl1 .row-num{background-color:lightgray;}
	#tbl1 .col-num{background-color:lightgray;}
"$
	CP.CSS(CSS)


	
	CP.AddTable.FromList(L).Id("tbl1").HasHeader.Highlight(3,HTMLPage_Static.HIGHLIGHT_BETWEEN,"yellow",0,2500) _
	.ColumnNumbers(0).RowNumbers.Title("Dump of data table")
	CP.FileName(File.DirTemp,"test1.html").ShowPage
	
	'Tidy up
	RS.close
	SQL.Close
	
End Sub

	'##########################################################################################
	'
	'				New Test Sync Scroll
	'
	'##########################################################################################

Public Sub SyncTest
	
	Dim CP As HTMLPage
	CP.Initialize
	
	CP.CSS($"${Addl_CSS.MultiViews}
	${Addl_CSS.MakeScrollable(".tbl-scroll")}
"$)
	
	Dim L,LL,LLL,L1,L2 As List
	L.Initialize
	LL.Initialize
	LLL.Initialize
	
	For i = 0 To 99
		Dim L1 As List
		L1.Initialize
		Dim L2 As List
		Dim L3 As List
		L3.Initialize
		L2.Initialize
		L1.Add("Table1 " & i)	
		L2.Add("Table2 " & i)
		L3.Add("Table3 " & i)
		
		L.Add(L1)
		LL.Add(L2)
		LLL.Add(L3)
	Next
	
	Dim Table1 As HTMLPage_Table =  CP.AddTable.FromList(L).DivClass("tbl-scroll")
	Dim Table2 As HTMLPage_Table =  CP.AddTable.FromList(LL).DivClass("tbl-scroll")
	Dim Table3 As HTMLPage_Table =  CP.AddTable.FromList(LLL).DivClass("tbl-scroll")
	
	CP.SyncScroll(Array(Table1,Table2,Table3),"tbl-scroll")
	
	CP.FileName(File.DirTemp,"test2.html").ShowPage
	
	
End Sub

	'##########################################################################################
	'
	'				New Test from a list of Types and highlights
	'
	'##########################################################################################
Private Sub Test1(CP As HTMLPage)
	
	Dim L As List
	L.Initialize
	
	For i = 0 To 30
		Dim HLT As HighlightType
		HLT.Initialize
		HLT.Colour = "Red"
		HLT.HLType = 0
		HLT.StartVal = Rnd(0,100) + 0.1
		HLT.EndVal = Rnd(0,100)
		l.Add(HLT)
	Next
	CP.AddTable.FromTypeList(L).Id("tbl1").DivClass("tbldiv1").Highlight(2,HTMLPage_Static.HIGHLIGHT_BETWEEN,"yellow",0,30) _
	.Highlight(3,HTMLPage_Static.HIGHLIGHT_BETWEEN,"lightgreen",31,60).Highlight(2,HTMLPage_Static.HIGHLIGHT_LT,"Orange",50,"") _
	.RowNumbers.Title("Test of Types").ColumnNumbers(0)
End Sub


	'##########################################################################################
	'
	'				New Test from a 2d array of Long as Hex and highlights
	'
	'##########################################################################################
Private Sub Test2(CP As HTMLPage)
	
	Dim C As Int = 8
	Dim R As Int = 30
	Dim TopVal As Int = 255
	Dim IIArr(R,C) As Byte
	For Ci = 0 To C-1
		For Ri = 0 To R-1
			IIArr(Ri,Ci) = Rnd(0,TopVal)
		Next
	Next

	CP.AddTable.FromArray(IIArr).ID("tbl2") _
  .RowNumbers.Highlight(2,HTMLPage_Static.HIGHLIGHT_BETWEEN,"Green",0,50).Highlight(6,HTMLPage_Static.HIGHLIGHT_LTEQ,"LightPink",30,0) _
  .Title($"2d array of byte in List</br>with header And index as Hex"$).AsHex
End Sub


	'##########################################################################################
	'
	'				New Test from a Single dimensional array
	'
	'##########################################################################################
Private Sub Test3(CP As HTMLPage)
	
	CP.AddTable.FromArray(Array As Double(1,2,3,4,5,6,7)).Title("Single Dimension Array of Double")
End Sub


	'##########################################################################################
	'
	'				New Test a 2d array of Boolean
	'
	'##########################################################################################
Private Sub Test4(CP As HTMLPage)
	
	Dim ZZArr(2,7) As Boolean
	For i = 0 To 6
		ZZArr(0,i) = True
		ZZArr(1,i) = False
	Next
	CP.AddTable.FromArray(ZZArr).Highlight(1,HTMLPage_Static.HIGHLIGHT_EQ,"Blue",True,"").Title("Multi Dimension Array of Boolean")
	
End Sub


	'##########################################################################################
	'
	'				New Test from delimited text
	'
	'##########################################################################################
Private Sub Test5(CP As HTMLPage)

	CP.AddTable.FromCSV($"101-2-3-4-5-6-7
8-9-10-11-12-13-14"$,"-").Title("Table from delimited text")
End Sub


	'##########################################################################################
	'
	'				New Test from a list of different type arrays / lists
	'
	'##########################################################################################
Private Sub Test6(CP As HTMLPage)
	
	Dim LL As List
	LL.Initialize
	
	'String Array
	Dim SL As List = Array As String("Column1","Column2","Column3","Column4","Column5","Column6","Column7")
	LL.Add(SL)
	Dim DL(7) As Double = Array As Double(1.4,200.65,51,99,300,1045,22000)
	LL.Add(DL)
	
	Dim ZZArr(2,7) As Boolean
	For i = 0 To 6
		ZZArr(0,i) = True
		ZZArr(1,i) = False
	Next

	'2D dimensional array as part of a bigger list
	'First convert the multi array into a list of arrays
	Dim Jo As JavaObject
	Jo.InitializeStatic("java.util.Arrays")
	Dim ZZL As List = Jo.RunMethod("asList",Array(ZZArr))

	'add the resultant single dimension arrays to the list
	For i = 0 To ZZL.Size - 1
		LL.Add(ZZL.Get(i))
		LL.Add(ZZL.Get(i))
	Next
	
	Dim B(7) As Byte
	For i = 0 To 6
		B(i) = Rnd(0,256)
	Next
	LL.Add(B)
	
	CP.AddTable.FromList(LL).Title("Multiple arrays in one table").HasHeader

End Sub


	'##########################################################################################
	'
	'				New Test a Div with text
	'
	'##########################################################################################
Private Sub Test7(CP As HTMLPage)

	CP.AddDiv.Id("div2").Content($"<span class="span1"">Nested Divs</span></br><div id="div3"><span class="span1">Test div</span> More text</br>And More</div>"$)

End Sub


	'##########################################################################################
	'
	'				New Test from CSV Text With Header and column number offset
	'
	'##########################################################################################
Private Sub Test8(CP As HTMLPage)

	CP.AddTable.FromCSV($"P1,P2,P3,P4,P5,P6,P7
	1,2,3,4,5,6,7
	8,9,10,11,12,13,14"$,",").ID("tbl3").Title("Table from CSV text").ColumnNumbers(1).HasHeader
End Sub

Private Sub ForDoc			'ignore
	
	Dim TestData As String = $"Test Data1,TestData2
TestData3,TestData4"$

	Dim CP As HTMLPage
	CP.Initialize.FileName(File.DirTemp,"doc.html")
	CP.AddTable.FromCSV(TestData,",").RowNumbers.ColumnNumbers(0)
	CP.ShowPage
Return
	
End Sub