AppType=JavaFX
Build1=Default,b4j.example
File1=countries.zip
File2=CountryItem.bjl
File3=Layout1.bjl
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=archiver
Library2=jcore
Library3=jfx
Library4=jxui
Library5=xui views
Library6=nitritenosqldb-b4xlib
Module1=AccessDB
Module2=NE_Utils
Module3=QueryDB
NumberOfFiles=3
NumberOfLibraries=6
NumberOfModules=3
Version=8.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 950
	#MainFormHeight: 700 
#End Region

'Data imported from https://github.com/samayo/country-json

#AdditionalJar: nitrite\nitrite-3.4.3
#AdditionalJar: nitrite\slf4j-api-1.7.30
#AdditionalJar: nitrite\slf4j-jdk14-1.7.30
#AdditionalJar: nitrite\h2-mvstore-1.4.200
#AdditionalJar: nitrite\jackson-core-2.12.1
#AdditionalJar: nitrite\jackson-databind-2.12.1
#AdditionalJar: nitrite\jackson-annotations-2.12.1
#AdditionalJar: nitrite\objenesis-3.1
#AdditionalJar: nitrite\podam-7.2.6.RELEASE

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI 
	Private AppName As String = "Nitrite Example"
	Private CLV As CustomListView
	Private btnFind As Button
	Public tfCountry As B4XView
	Public tfAbbrev As B4XView
	Public tfCity As B4XView
	Public tfContinent As B4XView
	Public tfCurrency As B4XView
	Public tfTempMin As B4XView
	Public tfTempMax As B4XView
	Public tfPopMax As B4XView
	Public tfPopMin As B4XView
	Private SortField As String
	Private SortAscending As Boolean = True
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	MainForm.Title = AppName & " Country Information"
	xui.SetDataFolder(AppName)
	
	SortField = AccessDB.DOC_COUNTRY
	
	If File.Exists(xui.DefaultFolder,"countries.db") = False Then
		Log("Updating database")
		Dim FileName As String  = "countries.zip"
		File.Copy(File.DirAssets,FileName,xui.DefaultFolder,FileName)
		Dim Arc As Archiver
		Arc.UnZip(xui.DefaultFolder,FileName,xui.DefaultFolder,"")
		File.Delete(xui.DefaultFolder,FileName)
	End If
	
	AccessDB.Initialize
	btnFind_Click
End Sub

Private Sub btnFind_Click
	UpdateTable(QueryDB.GetCountries(SortField,SortAscending))
End Sub

Private Sub lblSort_MouseClicked (EventData As MouseEvent)
	Dim lbl As B4XView = Sender
	If SortField = lbl.Tag Then
		SortAscending = Not(SortAscending)
	Else
		SortAscending = True
		SortField = lbl.Tag
	End If
	btnFind_Click
End Sub

Private Sub btnClear_Click
	For i = 0 To MainForm.RootPane.NumberOfNodes -1
		Dim V As B4XView = MainForm.RootPane.GetNode(i)
		If V.Tag = "NumberField" Then
			V.SetColorAndBorder(V.Color,2,xui.Color_Transparent,5)
			V.GetView(0).Text = ""
		Else If V Is TextField Then
			V.Text = ""
		End If
	Next
	SortField = AccessDB.DOC_COUNTRY
	btnFind.Enabled = True
	btnFind_Click
End Sub

Private Sub UpdateTable(Countries As List)
	CLV.Clear
	For Each Doc As NitriteDocument In Countries
		Dim P As B4XView = xui.CreatePanel("")
		P.SetLayoutAnimated(0,0,0,CLV.GetBase.Width,25)
		P.LoadLayout("CountryItem")
		P.GetView(0).Text = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_COUNTRY))
		P.GetView(1).Text = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_ABBREVIATION))
		P.GetView(2).Text = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_CITY))
		P.GetView(3).Text = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_CONTINENT))
		P.GetView(4).Text = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_CURRENCY_NAME))
		Dim Pop As String = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_POPULATION))
		If IsNumber(Pop) Then
			P.GetView(5).Text = NumberFormat2(Pop,0,0,0,True)
		Else
			P.GetView(5).Text = Pop
		End If
		P.GetView(6).Text = NE_Utils.NotNull(Doc.Get(AccessDB.DOC_TEMPERATURE))
		CLV.Add(P,Doc)
	Next
End Sub

Private Sub tfNumber_TextChanged (Old As String, New As String)
	Dim tf As B4XView = Sender
	
	If IsNumber(New) Or New = "" Then
		tf.Parent.SetColorAndBorder(tf.Color,2,xui.Color_Transparent,5)
		btnFind.Enabled = True
	Else
		tf.Parent.SetColorAndBorder(tf.Color,2,xui.Color_Red,5)
		btnFind.Enabled = False
	End If
End Sub

Private Sub tfNumber_Action
	btnFind_Click
End Sub

Private Sub tfText_Action
	btnFind_Click
End Sub