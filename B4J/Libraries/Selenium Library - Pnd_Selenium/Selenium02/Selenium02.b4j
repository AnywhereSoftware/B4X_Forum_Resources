AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=pnd_selenium
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=0
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 400
	#MainFormHeight: 200
	#MergeLibraries: False 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private Selenium As Pnd_Selenium
	Private LinkMap As Map
End Sub


	' *******************************************************************************
	' In this example:
	' How to get all links on the page with FindByTagNameList
    ' How to get all links on the page with FindByXPathList
    ' How to filter links
    ' How to get all TD text from all TABLES on all pages
    ' How to navigate from page to page without revisiting already visited links
	' *******************************************************************************


Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	Sleep(500) ' To show MainForm before Chrome
	
	Selenium.Initialize("Selenium", "e:\Kotlin\Wrap_Selenium\ChromeForTestingAndChromeDriver\chromedriver.exe")
	
	LinkMap.Initialize
	LinkMap.Put("https://www.w3schools.com/css/css_table.asp", 0) ' 0 = not visited, 1 = visited
		
	Selenium.NavigateTo(LinkMap.GetKeyAt(0))
End Sub


Sub Selenium_NavigationFinished
	' After page is loaded we can get all links from the page
	' GetAllLinksFromThePageWithCss_TableTextInURL1 ' - Find links with FindByTagNameList
	GetAllLinksFromThePageWithCss_TableTextInURL2  '  - Find links with FindByXPathList
	
	Log ("<TD> text from: " & Selenium.CurrentUrl)
	Dim ListOfTABLEtags As List = Selenium.FindByTagNameList("table")                ' we are looking to find every table on the page, ListOfTABLEtags now represent List of TABLEs from tag <table> to </table>
	For Each OneTABLEtag As Pnd_WebElement In ListOfTABLEtags                        ' OneTABLEtag now represent one TABLE from tag <table> to </table>
		Dim AllTdTextFromTable As String = ""
		Dim ListoOfTRtags As List = OneTABLEtag.FindByTagNameList("tr")              ' here we are get list of ROWS inside OneTABLEtag - ListoOfTRtags now represent List of ROWS from tag <tr> to </tr> inside OneTABLEtag
		For Each OneTRtag As Pnd_WebElement In ListoOfTRtags                         ' inside ListoOfTRtags we are extracting each OneTRtag one by one
			Dim ListOfTDtags As List = OneTRtag.FindByTagNameList("td")              ' inside OneTRtag we are searching for <TD> tags
			For Each OneTDtag As Pnd_WebElement In ListOfTDtags                      ' now we have a List od TD tags, it's time to extract text from TD tags one by one
				AllTdTextFromTable = AllTdTextFromTable & "   " & OneTDtag.GetText   ' we can get text from TD tag
			Next
			Log(AllTdTextFromTable.Trim)
			AllTdTextFromTable = ""                                                  ' all TD tags text in TR tag is now extracted, reset text to ""
		Next
	Next
		
	LinkMap.Put(Selenium.CurrentUrl, 1)                ' set URL/Link as visited

	Dim NextUnvisitedLink As String = ""
	For i = 0 To LinkMap.Size - 1
		Dim MapValue As Int = LinkMap.GetValueAt(i)
		If MapValue = 0 Then                           ' we are looking for next unvisited link
			NextUnvisitedLink = LinkMap.GetKeyAt(i)
			Exit
		End If
	Next
	
	If NextUnvisitedLink = "" Then
		Selenium.Quit
		Sleep(500)
		ExitApplication
	Else
		Selenium.NavigateTo(NextUnvisitedLink)
	End If
End Sub


' Find links with FindByTagNameList - Slow process (few seconds) 
Sub GetAllLinksFromThePageWithCss_TableTextInURL1
	'Log("START PARSING: " & DateTime.Time(DateTime.Now))
	Dim AtagWebElementList As List = Selenium.FindByTagNameList("a")
	For Each OneAtag As Pnd_WebElement In AtagWebElementList            ' we have found all <A></A> tags, we need to get <A> tags one by one
		Dim URL As String = OneAtag.GetDomProperty("href")              ' extracting link from HREF tag inside <A></A> tag
		'Dim URL As String = OneAtag.GetDomAttribute("href")            ' GetDomAttribute will return only the last part of link like css_table.asp
		
		' Now we can filter URL/Links to get only links that we want
		If LinkMap.ContainsKey(URL)=False And URL.Contains("css_table")=True And URL.Contains("profile.")=False And URL.Contains("filename")=False Then
			LinkMap.Put(URL, 0)
		End If
	Next
	'Log("FINISH PARSING: " & DateTime.Time(DateTime.Now))
End Sub


' Find links with FindByXPathList - Slow process (few seconds) 
Sub GetAllLinksFromThePageWithCss_TableTextInURL2
	'Log("START PARSING: " & DateTime.Time(DateTime.Now))
	Dim AtagWebElementList As List = Selenium.FindByXPathList("//a[@href]")
	For Each OneAtag As Pnd_WebElement In AtagWebElementList                  ' we have found all <A></A> tags, we need to get <A> tags one by one
		Dim URL As String = OneAtag.GetDomProperty("href")                    ' extracting link from HREF tag inside <A></A> tag
		'Dim URL As String = OneAtag.GetDomAttribute("href")                  ' GetDomAttribute will return only the last part of link like css_table.asp
		
		' Now we can filter URL/Links to get only links that we want
		If LinkMap.ContainsKey(URL)=False And URL.Contains("css_table")=True And URL.Contains("profile.")=False And URL.Contains("filename")=False Then
			LinkMap.Put(URL, 0)
		End If
	Next
	'Log("FINISH PARSING: " & DateTime.Time(DateTime.Now))
End Sub