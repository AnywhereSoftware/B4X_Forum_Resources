AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
File2=table.bjl
FileGroup1=Default Group
FileGroup2=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jxui
Library4=b4xtable
NumberOfFiles=2
NumberOfLibraries=4
NumberOfModules=0
Version=10.3
@EndOfDesignText@
#Region Changes
'- 2025-09-13: added ButtonCopy to copy current settings' code to the clipboard
#End Region ' Changes

#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600
	#VirtualMachineArgs: -Db4j.simplelogs=true
#End Region

Sub Process_Globals

	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI 

	Private fontArial_11 As Font
	Private fontArial_11bold As Font

	Private currentSettings As String = ""

	' Variables for "Adjust the width of column 2 to its contents"
	Private column2 As B4XTableColumn
	Private cvs As B4XCanvas

	' Designer Views
	Private TextFieldRows As B4XView
	Private TextFieldColumns As B4XView
	Private CheckBox1 As CheckBox
	Private ChoiceBox1 As ChoiceBox
	Private Pane1 As B4XView
	Private B4XTable1 As B4XTable

End Sub

Sub AppStart (Form1 As Form, Args() As String)

	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	Pane1.LoadLayout("table")
	MainForm.Show

	fontArial_11 = fx.CreateFont("ARIAL", 11, False, False)
	fontArial_11bold = fx.CreateFont("ARIAL", 11, True, False)

	Dim p As B4XView = xui.CreatePanel("")
	p.SetLayoutAnimated(0, 0, 0, 1dip, 1dip)
	cvs.Initialize(p)

	ChoiceBox1.Items.Clear
	ChoiceBox1.Items.Add("Reset")
	ChoiceBox1.Items.Add("--- Properties ---")
	ChoiceBox1.Items.Add("AllowSmallRowHeightModifications = False")
	ChoiceBox1.Items.Add("ArrowsDisabledColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("ArrowsEnabledColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("Columnwidths = 25dip")
	ChoiceBox1.Items.Add("EvenRowColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("FirstRowIndex = 50")
	ChoiceBox1.Items.Add("GridColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("HeaderColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("HeaderFont = fontArial_11bold")
	ChoiceBox1.Items.Add("HeadersHeight = 20dip")
	ChoiceBox1.Items.Add("HeaderTextColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("HighlightSearchResults = True; HighlightTextColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("LabelsFont = fontArial_11")
	ChoiceBox1.Items.Add("lblBack.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("lblFirst.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("lblFromTo.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("lblLast.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("lblNext.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("lblNumber.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("lblSort.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("MaximumRowsPerPage = 5")
	ChoiceBox1.Items.Add("Navigation panel formatting")
	ChoiceBox1.Items.Add("NumberOfFrozenColumns = 1")
	ChoiceBox1.Items.Add("OddRowColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("pnlHeader.Color = xui.Color_Magenta")
	'ChoiceBox1.Items.Add("pnlTitles.Color = xui.Color_Magenta") ' Don't know why, but applying that change throws this exception: java.lang.RuntimeException: Object should first be initialized (B4XView).
	ChoiceBox1.Items.Add("RowHeight = 100dip")
	ChoiceBox1.Items.Add("SearchField.TextField.Color = xui.Color_Magenta")
	ChoiceBox1.Items.Add("SearchVisible = False")
	ChoiceBox1.Items.Add("SelectionColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("StringMoreAvailable = 'MOOOORE'")
	ChoiceBox1.Items.Add("StringNoMatches = 'NOOOOONE'")
	ChoiceBox1.Items.Add("StringOutOf = 'OUTOFFFF'")
	ChoiceBox1.Items.Add("StringTo = 'TOOOOOO'")
	ChoiceBox1.Items.Add("TextColor = xui.Color_Magenta")
	ChoiceBox1.Items.Add("--- Other actions ---")
	ChoiceBox1.Items.Add("Adjust the width of column 2 to its contents")
	ChoiceBox1.Items.Add("Hide column 3")
	ChoiceBox1.Items.Add("Next page (navigate without the navigation buttons)")
	ChoiceBox1.Items.Add("Previous page (navigate without the navigation buttons)")
	ChoiceBox1.Items.Add("Right-align contents")
	ChoiceBox1.Items.Add("Show column 3")
	ChoiceBox1.Items.Add("Wrap text")

	CustomizeB4XTable1("")

End Sub

Private Sub ChoiceBox1_SelectedIndexChanged(Index As Int, Value As Object)

	CustomizeB4XTable1(Value.As(String))

End Sub

Private Sub CustomizeB4XTable1(changeThis As String)

	Dim numRows As Int = IIf(IsNumber(TextFieldRows.Text), TextFieldRows.Text, 100)
	Dim numColumns As Int = IIf(IsNumber(TextFieldColumns.Text), TextFieldColumns.Text, 100)
	Dim iRow As Int
	Dim iCol As Int
	Dim columnWidth As Double = -1
	Dim data As List

	If changeThis.StartsWith("-") Then Return ' Separator for ChoiceBox1, no action

	' Just because I want at least 10 rows and columns here
	If numRows < 10 Then numRows = 10
	If numColumns < 10 Then numColumns = 10

	' Reset B4XTable1 to its Designer state?
	If CheckBox1.Checked Then
		Pane1.RemoveAllViews
		Pane1.LoadLayout("table")
		Sleep(0)
	End If

	' Initialise B4XTable1
	B4XTable1.Clear ' Not needed in this example, but would be useful in a program where new data can be loaded
	B4XTable1.MaximumRowsPerPage = IIf(changeThis = "MaximumRowsPerPage = 5", 5, 20) ' First customisation, done before BuildLayoutsCache
	B4XTable1.BuildLayoutsCache(B4XTable1.MaximumRowsPerPage)
	Sleep(0) ' Important, otherwise e.g. lblFirst won't have been initialised yet in the next block

	' Customisations
	Select Case changeThis
		Case "Reset"
			currentSettings = ""
		Case "AllowSmallRowHeightModifications = False"
			B4XTable1.AllowSmallRowHeightModifications = False ' Default value is True
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "ArrowsDisabledColor = xui.Color_Magenta"
			B4XTable1.ArrowsDisabledColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "ArrowsEnabledColor = xui.Color_Magenta"
			B4XTable1.ArrowsEnabledColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "Columnwidths = 25dip"
			columnWidth = 25dip ' This is used further down
		Case "EvenRowColor = xui.Color_Magenta"
			B4XTable1.EvenRowColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "FirstRowIndex = 50"
			B4XTable1.FirstRowIndex = 50
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "GridColor = xui.Color_Magenta"
			B4XTable1.GridColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "HeaderColor = xui.Color_Magenta"
			B4XTable1.HeaderColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "HeaderFont = fontArial_11bold"
			B4XTable1.HeaderFont = fontArial_11bold
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "HeadersHeight = 20dip"
			B4XTable1.HeadersHeight = 20dip
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "HeaderTextColor = xui.Color_Magenta"
			B4XTable1.HeaderTextColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "HighlightSearchResults = True; HighlightTextColor = xui.Color_Magenta"
			B4XTable1.HighlightSearchResults = True
			B4XTable1.HighlightTextColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1.HighlightSearchResults = True" & CRLF	& "B4XTable1.HighlightTextColor = xui.Color_Magenta"
			fx.Msgbox(MainForm, "Perform a search and check the highlights", "Info")
		Case "LabelsFont = fontArial_11"
			B4XTable1.LabelsFont = fontArial_11
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblBack.Color = xui.Color_Magenta"
			B4XTable1.lblBack.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblFirst.Color = xui.Color_Magenta"
			B4XTable1.lblFirst.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblFromTo.Color = xui.Color_Magenta"
			B4XTable1.lblFromTo.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblLast.Color = xui.Color_Magenta"
			B4XTable1.lblLast.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblNext.Color = xui.Color_Magenta"
			B4XTable1.lblNext.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblNumber.Color = xui.Color_Magenta"
			B4XTable1.lblNumber.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "lblSort.Color = xui.Color_Magenta"
			B4XTable1.lblSort.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
			fx.Msgbox(MainForm, "Click any column's header to make lblSort appear", "Info")
		Case "Navigation panel formatting" ' https://www.b4x.com/android/forum/threads/b4xtable-reduce-page-navigation-size-and-font-size-and-width-of-column-header.142264/#post-901504
			Dim pnlNav As B4XView = B4XTable1.lblNumber.Parent
			pnlNav.Color = xui.Color_Magenta
			pnlNav.Height = 30dip
			For Each v As B4XView In pnlNav.As(Pane)
				If v Is Label Then
					v.Height = 20dip
					v.TextSize = 16
				End If
			Next
			currentSettings = currentSettings & CRLF & CRLF & $"
'"Navigation panel formatting" ' https://www.b4x.com/android/forum/threads/b4xtable-reduce-page-navigation-size-and-font-size-and-width-of-column-header.142264/#post-901504
Dim pnlNav As B4XView = B4XTable1.lblNumber.Parent
pnlNav.Color = xui.Color_Magenta
pnlNav.Height = 30dip
For Each v As B4XView In pnlNav.As(Pane)
	If v Is Label Then
		v.Height = 20dip
		v.TextSize = 16
	End If
Next"$
		Case "NumberOfFrozenColumns = 1"
			B4XTable1.NumberOfFrozenColumns = 1
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "OddRowColor = xui.Color_Magenta"
			B4XTable1.OddRowColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "pnlHeader.Color = xui.Color_Magenta"
			B4XTable1.pnlHeader.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "RowHeight = 100dip"
			B4XTable1.RowHeight = 100dip
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "SearchField.TextField.Color = xui.Color_Magenta"
			B4XTable1.SearchField.TextField.Color = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "SearchVisible = False"
			B4XTable1.SearchVisible = False
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "SelectionColor = xui.Color_Magenta"
			B4XTable1.SelectionColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
			fx.Msgbox(MainForm, "Click a cell to see what happens", "Info")
		Case "StringMoreAvailable = 'MOOOORE'"
			B4XTable1.StringMoreAvailable = "MOOOORE"
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
			fx.Msgbox(MainForm, "Enter a nonexistent value in the SearchBox to see what happens", "Info")
		Case "StringNoMatches = 'NOOOOONE'"
			B4XTable1.StringNoMatches = "NOOOOONE"
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
			fx.Msgbox(MainForm, "Enter a nonexistent value in the SearchBox to see what happens", "Info")
		Case "StringOutOf = 'OUTOFFFF'"
			B4XTable1.StringOutOf = "OUTOFFFF"
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "StringTo = 'TOOOOOO'"
			B4XTable1.StringTo = "TOOOOOO"
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
		Case "TextColor = xui.Color_Magenta"
			B4XTable1.TextColor = xui.Color_Magenta
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1." & changeThis
	End Select

	Sleep(0) ' Let the UI update

	' Create the columns
	For iCol = 0 To (numColumns - 1)
		Dim col As B4XTableColumn = B4XTable1.AddColumn("Col " & iCol, B4XTable1.COLUMN_TYPE_TEXT)
		If changeThis = "Adjust the width of column 2 to its contents" Then
			If iCol = 2 Then
				column2 = col
			End If
		End If
		If columnWidth > 0 Then col.Width = columnWidth
		col.Id = "ID" & iCol.As(String) ' Can be picked up later on in code, like a tag; doesn't make sense if it's just an index, but it could be e.g. a date
	Next

	' Prepare the data for B4XTable1
	data.Initialize
	For iRow = 1 To numRows
		Dim row(numColumns) As Object
		row(0) = NumberFormat2(iRow, 2, 0, 0, False) ' The first column contains the hour
		For iCol = 1 To numColumns
			row(iCol - 1) = "R=" & (iRow - 1) & ", C=" & (iCol - 1) & IIf(changeThis = "Wrap text", ", and this is some more text to demonstrate 'Wrap text'", "")
		Next
		data.Add(row)
	Next

	' Populate B4XTable1 with data
	Wait For (B4XTable1.SetData(data)) Complete (Unused As Boolean)
	Sleep(0) ' Let the UI update

	' Format the cells
	Select Case changeThis
		Case "Right-align contents", "Wrap text"
			For iCol = 0 To (numColumns - 1)
				Dim col As B4XTableColumn = B4XTable1.Columns.Get(iCol).As(B4XTableColumn)
				B4XTableSetCellProperties(col, changeThis = "Wrap text", changeThis = "Right-align contents")
			Next
			currentSettings = currentSettings & CRLF & CRLF & $"
' "Right-align contents", "Wrap text"
' Sub B4XTableSetCellProperties can be found in the source code
For iCol = 0 To (numColumns - 1)
	Dim col As B4XTableColumn = B4XTable1.Columns.Get(iCol).As(B4XTableColumn)
	B4XTableSetCellProperties(col, changeThis = "Wrap text", changeThis = "Right-align contents")
Next"$
	End Select
	Sleep(0) ' Let the UI update

	' Refresh B4XTable1
	B4XTable1.RefreshNow

	Select Case changeThis
		Case "Next page (navigate without the navigation buttons)"
			B4XTable1.CurrentPage = B4XTable1.CurrentPage + 1
			currentSettings = currentSettings & CRLF & CRLF & "B4XTable1.CurrentPage = B4XTable1.CurrentPage + 1 ' Next page (navigate without the navigation buttons)"
		Case "Previous page (navigate without the navigation buttons)"
			If B4XTable1.CurrentPage > 0 Then B4XTable1.CurrentPage = B4XTable1.CurrentPage - 1
			currentSettings = currentSettings & CRLF & CRLF & "If B4XTable1.CurrentPage > 0 Then B4XTable1.CurrentPage = B4XTable1.CurrentPage - 1 ' Previous page (navigate without the navigation buttons)"
	End Select

	' Hide/show columns - see https://www.b4x.com/android/forum/threads/solved-b4xtable-dynamically-show-hide-column.113504
	Select Case changeThis
		Case "Hide column 3"
			HideColumn("ID3") ' Note: the column's ID property must be passed to this method, not e.g. its number
			currentSettings = currentSettings & CRLF & CRLF & "'Column 3 was hidden; check Sub HideColumn in the source code to see how that was done"
		Case "Show column 3"
			ShowColumn("ID3", 3) ' Note: the column's ID property must be passed to this method, not e.g. its number
			currentSettings = currentSettings & CRLF & CRLF & "'Column 3 was made visible; check Sub ShowColumn in the source code to see how that was done"
	End Select

End Sub

' Note: the same approach can be used to change properties other than 'WrapText' and 'Alignment' of the cell labels
Private Sub B4XTableSetCellProperties(Col As B4XTableColumn, setWrapText As Boolean, setAlignment As Boolean)

	' Code based on
	' https://www.b4x.com/android/forum/threads/b4xtable-wrap-text-in-rows.130005
	' https://www.b4x.com/android/forum/threads/how-to-set-text-alignment-in-b4xtable.103870

	For i = 1 To Col.CellsLayouts.Size - 1
		Dim p As B4XView = Col.CellsLayouts.Get(i)
		Dim lbl As Label = p.GetView(Col.LabelIndex)
		If setWrapText Then lbl.WrapText = True
		If setAlignment Then lbl.Alignment = "TOP_RIGHT"
	Next

End Sub

' Note: in this example, this is only used for column 2, if "Adjust the width of column 2 to its contents" is selected
Private Sub B4XTable1_DataUpdated

	' Code from https://www.b4x.com/android/forum/threads/b4x-b4xtable-resize-columns-based-on-content.102678

	If column2.IsInitialized = False Then Return ' Only applying this to column 2 if "Adjust the width of column 2 to its contents" is selected

	Dim ShouldRefresh As Boolean
	For Each column As B4XTableColumn In Array(column2)
		Dim MaxWidth As Int
		For i = 0 To B4XTable1.VisibleRowIds.Size - 1
			Dim RowId As Long = B4XTable1.VisibleRowIds.Get(i)
			If RowId > 0 Then
				Dim pnl As B4XView = column.CellsLayouts.Get(i + 1)
				Dim lbl As B4XView = pnl.GetView(0)
				Dim txt As String = B4XTable1.GetRow(RowId).Get(column.Id)
				MaxWidth = Max(MaxWidth, cvs.MeasureText(txt, lbl.Font).Width + 10dip)
			End If
		Next
		If MaxWidth > column.ComputedWidth Or MaxWidth < column.ComputedWidth - 20dip Then
			column.Width = MaxWidth
			ShouldRefresh = True
		End If
	Next
	If ShouldRefresh Then
		B4XTable1.Refresh
	End If

End Sub

' Hides the requested column from view.
' Note: the column's ID property must be passed to this method, not e.g. its number
Public Sub HideColumn(ColID As String)
	' Code from https://www.b4x.com/android/forum/threads/solved-b4xtable-dynamically-show-hide-column.113504/#post-708428
	Dim poCol As B4XTableColumn = B4XTable1.GetColumn(ColID)
	If poCol <> Null And poCol.IsInitialized Then
		Dim piVisibleColIndex As Int = B4XTable1.VisibleColumns.IndexOf(poCol)
		If piVisibleColIndex >= 0 Then
			B4XTable1.VisibleColumns.RemoveAt(piVisibleColIndex) 'hide column
			If B4XTable1.clvData.IsInitialized Then
				If B4XTable1.clvData.Size > piVisibleColIndex Then
					B4XTable1.clvData.RemoveAt(piVisibleColIndex)
				End If
			End If
			poCol.Panel.RemoveViewFromParent
		End If
		B4XTable1.Refresh
	End If
End Sub

' Shows a hidden column, if InsertIndex = -1 then the column will be added to the end of the column list.
' Note: the column's ID property must be passed to this method, not e.g. its number
Public Sub ShowColumn(ColID As String, InsertIndex As Int)
	' Code from https://www.b4x.com/android/forum/threads/solved-b4xtable-dynamically-show-hide-column.113504/#post-708428
	Dim poCol As B4XTableColumn = B4XTable1.GetColumn(ColID)
	If poCol <> Null And poCol.IsInitialized Then
		Dim piVisibleColIndex As Int = B4XTable1.VisibleColumns.IndexOf(poCol)
		If piVisibleColIndex < 0 Then
			If InsertIndex < 0 Then
				B4XTable1.VisibleColumns.Add(poCol)
				B4XTable1.clvData.Add(poCol.Panel, poCol)
			Else
				B4XTable1.VisibleColumns.InsertAt(InsertIndex, poCol)
				B4XTable1.clvData.InsertAt(InsertIndex, poCol.Panel, poCol)
			End If
		End If
		B4XTable1.Refresh
	End If
End Sub

Private Sub ButtonCopy_Click

	fx.Clipboard.SetString(currentSettings)

End Sub