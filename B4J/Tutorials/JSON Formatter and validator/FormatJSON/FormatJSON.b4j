AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=javaobject
Library2=jcore
Library3=jfx
Library4=json
Library5=jxui
Library6=xui views
Module1=FormatJSON
NumberOfFiles=1
NumberOfLibraries=6
NumberOfModules=1
Version=9.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI 
	Private Button1 As B4XView
	Private TextArea1 As B4XView
	Private Dialog As B4XDialog
	Private lblCaretPos As B4XView
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	Dialog.Initialize(MainForm.RootPane)
	TextArea1.Text = JSON
	MainForm.Show
	
	'Listener to determine the caret position for the TextArea
	Dim Prop As JavaObject = TextArea1.As(JavaObject).RunMethod("caretPositionProperty",Null)
	Dim CL As Object = Prop.CreateEventFromUI("javafx.beans.value.ChangeListener","CaretMoved",Null)
	Prop.RunMethod("addListener",Array(CL))
End Sub

'Format JSON
Sub Button1_Click
	Dim FJ As FormatJSON
	FJ.Initialize(4)
	TextArea1.Text = FJ.FormatJSON(TextArea1.Text,True)
	If FJ.ISValid = False Then
		Dialog.Title = "Validate JSON"
		Dim TT As B4XLongTextTemplate
		TT.Initialize
		TT.Text = FJ.ErrorMessage
		Wait For(Dialog.ShowTemplate(TT,"OK","","")) Complete (Resp As Int)
		TextArea1.SetSelection(FJ.ErrorPos - 1,1)
	End If
End Sub

'Validate JSON
Sub Button2_Click
	Dim FJ As FormatJSON
	FJ.Initialize(4)
	FJ.ValidateJSON(TextArea1.Text)
	Dialog.Title = "Validate JSON"
	If FJ.ISValid Then
		Wait For(Dialog.Show("Text appears to be vaild json","OK","","")) Complete (Resp As Int)
	Else
		Dim TT As B4XLongTextTemplate
		TT.Initialize
		TT.Text = FJ.ErrorMessage
		Wait For(Dialog.ShowTemplate(TT,"OK","","")) Complete (Resp As Int)
		TextArea1.SetSelection(FJ.ErrorPos - 1,1)
	End If
End Sub

Private Sub JSON As String
	Return $"
	    [
    {
        "name": "get_names",
        "description": "List of names",
        "parameters": {
            "type": "object",
            "properties": {
                "firstname": {
                    "description": "First Name",
                    "type": "string"
                },
                "lastname": {
                    "description": "Last Name",
                    "type": "string"
                }
            },
        "required": ["firstname","lastname"]
        }
    }
]"$
End Sub

Private Sub CaretMoved_Event (MethodName As String, Args() As Object)
	If TextArea1.SelectionLength = 0 Then
		lblCaretPos.Text = Args(2)
	Else
		lblCaretPos.Text = TextArea1.SelectionStart & "-" & (TextArea1.SelectionStart + TextArea1.SelectionLength)
	End If
End Sub