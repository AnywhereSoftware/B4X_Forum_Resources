AppType=JavaFX
Build1=Default,com.stevel05.jAudioTrack2
File1=1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=javaobject
Library2=jcore
Library3=jdateutils
Library4=jfx
Library5=jshell
Library6=jxui
Library7=xui views
Library8=jaudiotrack2-b4xlib
Module1=FFMPeg_Utils
NumberOfFiles=1
NumberOfLibraries=8
NumberOfModules=1
Version=9.8
@EndOfDesignText@
'Archive Modules: ide://run?File=C:\Java\jdk1.8.0_271\bin\java.exe&Args=-jar&Args=C:\NoInstApps\B4j\ArchiveProjectModules\ArchiveProjectModules.jar&Args=%PROJECT%\jAudioTrack2.b4j

#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region


#If Version
V0.1 
	Initial Version
VC0.11
	Added NewAudioInputStreamFromBytes to jAudioTrack2_Utils to play from bytes array.
#End If

Sub Process_Globals
	
	Private XUI As XUI
	Private MainForm As Form
	Private InitialFilePath As String
	Private FileName As String
	Private BtnFile As Button
	Private lblFileName As Label
	Private btnPlay As B4XView
	Private btnStop As B4XView
	Private btnPause As B4XView
	Private tfURL As TextField
	
	Private Devices As List
	
	Private AIS As JavaObject
	Private Clip1 As ClipWrapper
	
	Private cbOutputs As B4XComboBox
	
	Private SelectedMixerInfo As JavaObject
	Private sbPosition As B4XSeekBar
	Private lblTime As B4XView
	Private lblDuration As B4XView
	
	Private PosTimer As Timer
	
	Private Paused As Boolean
	
	Private Reload As Boolean
	
	Private btnFfmpeg As B4XView
	Private lblFfmpeg As B4XView
	
	Private Dialog As B4XDialog
	Private ConvertedPath As String
	Private ConvertedFile As String
	
	Private Options As Map
End Sub


Sub AppStart (Form1 As Form, Args() As String)
	
	XUI.SetDataFolder("jAdudioTrack2-Ffmpeg")
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1") 'Load the layout file.
	MainForm.Show
	Dialog.Initialize(MainForm.RootPane)
	
	Options.Initialize
	
	If File.Exists(XUI.DefaultFolder,"jat.opts") Then Options = File.ReadMap(XUI.DefaultFolder,"jat.opts")
	lblFfmpeg.Text = Options.GetDefault("PathToFFMPeg","")
	
	
	
'	btnPlay.Enabled=False

	'For file dialog
	InitialFilePath = Options.GetDefault("InitialFilePath", GetSystemProperty("user.home",""))
	
	'Setup Device names in a B$XCombobox and MixerInfo objects in a list
	Dim MixerInfos As List = jAudioTrack2_Utils.GetDevices(jAudioTrack2_Utils.DEVICETYPE_OUTPUT,"")
	
	Dim L As List
	L.Initialize
	Devices.Initialize
	For Each MI As JavaObject In MixerInfos
		'Add the name to the list for the B4xCombobox
		L.add(MI.RunMethod("getName",Null))
		'Add the MixerInfo to the corresponding Devices List
		Devices.Add(MI)
	Next
	
	'Set the list for the B4xCombobox
	cbOutputs.SetItems(L)
	cbOutputs_SelectedIndexChanged (0)
	
	PosTimer.Initialize("Postimer",1000)
End Sub

'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
	Return True
End Sub

Private Sub MainForm_CloseRequest (EventData As Event)
	File.Delete(File.DirTemp,"temp.wav")
	If Clip1.IsInitialized And Clip1.IsRunning Then
		PosTimer.Enabled = False
		Clip1.Stop
	End If
	File.WriteMap(XUI.DefaultFolder,"jat.opts",Options)
		
End Sub

Sub BtnFile_Click
	
	'Get a file to play from the user
	Dim FC As FileChooser
	
	FC.Initialize
	FC.Title="Choose an Audio file"
	FC.SetExtensionFilter("Audio files",Array As String("*.wav","*.flac","*.mp3","*.ogg"))
	FC.InitialDirectory = InitialFilePath
	Dim response As String = FC.ShowOpen(MainForm)
	If response = "" Then Return
	
	If InitialFilePath = File.GetFileParent(response) And FileName =  File.GetName(response) Then
		Return
	End If
	Reload = True
	InitialFilePath = File.GetFileParent(response)
	FileName =  File.GetName(response)
	lblFileName.Text=File.Combine(InitialFilePath,FileName)
	
	Options.Put("InitialFilePath",InitialFilePath)
	
	If FileName.ToLowerCase.EndsWith(".wav") = False Then
		If lblFfmpeg.Text = "" Then
			Dialog.Title = "Path to FFMPeg"
			Wait for (Dialog.Show("Please set path to FFMpeg to play this file","","","OK")) Complete (resp As Int)
			Return
		End If
		ConvertedFile = "temp.wav"
		ConvertedPath = File.DirTemp
		FFMPeg_Utils.Convert(lblFfmpeg.Text,InitialFilePath,FileName,ConvertedPath,ConvertedFile)
		
	Else
		ConvertedFile = ""
	End If
	
End Sub


Sub btnStop_Click
	PosTimer.Enabled = False
	Clip1.Stop
	Clip1.SetMicrosecondPosition(0)
	sbPosition.Value = 0
	Paused = False
End Sub

Private Sub btnPause_Click
	Paused = True
	PosTimer.Enabled = False
	Clip1.Stop
End Sub

Sub btnPlay_Click
	
	If Clip1.IsInitialized Then
		If Clip1.IsRunning Then
			Return
		End If
		If Paused Then
			PosTimer.Enabled = True
			Clip1.Start
			Return
		End If
	End If
	
	If Reload = False Then
		PosTimer.Enabled = True
		Clip1.Start
		Return
	End If
	
	Reload = False
	
	Dim PlayFile,PlayPath As String
	If ConvertedFile <> "" Then
		PlayPath = ConvertedPath
		PlayFile = ConvertedFile
	Else
		PlayPath = InitialFilePath
		PlayFile = FileName
	End If
	
	If AIS.IsInitialized Then AIS.RunMethod("close",Null)
	
	Dim AIS As JavaObject
	
	If tfURL.Text <> "" Then
		If tfURL.Text.ToLowerCase.StartsWith("http") Then
			AIS = jAudioTrack2_Utils.GetAudioInputStream3(tfURL.Text)
		Else
			AIS = jAudioTrack2_Utils.GetAudioInputStream3(File.GetUri(tfURL.Text,"UTF-8"))
		End If
	Else If FileName <> "" Then
		Log("Playing " & File.Combine(PlayPath,PlayFile))
		AIS = jAudioTrack2_Utils.GetAudioInputStream(PlayPath,PlayFile)
		
	End If
	
	If AIS.IsInitialized = False Then Return
	
	Clip1 = jAudioTrack2_Utils.GetClip(SelectedMixerInfo)
	Clip1.Open2(AIS)
	
	Clip1.AddLineListener(Me,"Clip1")
	
	Dim Duration_ms As Long = Clip1.GetMicrosecondLength / 1000
	lblDuration.Text = ConvertMillisecondsToString(Duration_ms)
	
	sbPosition.MinValue = 0
	sbPosition.MaxValue = Duration_ms
	sbPosition.Value = 0
	
	PosTimer.Enabled = True
	Clip1.Start
End Sub

Private Sub Clip1_Event(Name As String)
	'You can use the event to change the gui if needed. Play/Stop button maybe
	Log("Clip1Event " & Name)
End Sub

Private Sub cbOutputs_SelectedIndexChanged (Index As Int)
	
	If Clip1.IsInitialized And Clip1.IsRunning Then
		PosTimer.Enabled = False
		Clip1.Stop
		sbPosition.Value = 0
	End If
	
	'Get the selected Mixer Object from the Devices List
	SelectedMixerInfo = Devices.Get(Index)
	
	Log("Changed " & cbOutputs.GetItem(Index))
	
End Sub

Private Sub PosTimer_Tick
	Dim Pos As Long = Clip1.GetMicrosecondPosition / 1000
	sbPosition.Value = Pos
	lblTime.Text = ConvertMillisecondsToString(Pos)
End Sub

Sub ConvertMillisecondsToString(t As Long) As String
	Dim hours, minutes, seconds As Int
	hours = t / DateTime.TicksPerHour
	minutes = (t Mod DateTime.TicksPerHour) / DateTime.TicksPerMinute
	seconds = (t Mod DateTime.TicksPerMinute) / DateTime.TicksPerSecond
	Return $"$1.0{hours}:$2.0{minutes}:$2.0{seconds}"$
End Sub

Private Sub tfURL_FocusChanged (HasFocus As Boolean)
	If HasFocus Then
		tfURL.Tag = tfURL.Text
	Else
		If tfURL.Text <> tfURL.Tag Then
			Reload = True
		End If
	End If
End Sub

Private Sub sbPosition_ValueChanged (Value As Int)
	Clip1.SetMicrosecondPosition(Value * 1000)
	PosTimer_Tick
End Sub


Private Sub btnBack_Click
	If Clip1.AsJavaObject.IsInitialized And Clip1.IsRunning Then
		Clip1.SetMicrosecondPosition(Clip1.GetMicrosecondPosition + Skip("back"))
	End If
End Sub

Private Sub btnFwd_Click
	If Clip1.AsJavaObject.IsInitialized And Clip1.IsRunning Then
		Clip1.SetMicrosecondPosition(Clip1.GetMicrosecondPosition + Skip("fwd"))
	End If
End Sub

Private Sub Skip(Dir As String) As Long
	Dim TenSeconds As Long = 10000000
	Return IIf(Dir = "fwd",TenSeconds,-TenSeconds)
End Sub

Private Sub btnFfmpeg_Click
	Dim FC As FileChooser
	
	FC.Initialize
	FC.Title="Path to FFMpeg.exe"
	FC.SetExtensionFilter("Exe Files",Array As String("*.exe"))
	FC.InitialDirectory = InitialFilePath
	Dim response As String = FC.ShowOpen(MainForm)
	If response = "" Then Return
	If File.GetName(response).ToLowerCase <> "ffmpeg.exe" Then
		Dialog.Title = "Path error"
		Wait For(Dialog.Show("File must be ffnpeg.exe","","","OK")) Complete (Resp As Int)
		Return
	End If
	
	lblFfmpeg.Text = response
	Options.Put("PathToFFMPeg",response)
End Sub