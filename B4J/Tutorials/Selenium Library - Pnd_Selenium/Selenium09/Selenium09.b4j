AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=pnd_selenium
Library4=jstringutils
NumberOfFiles=1
NumberOfLibraries=4
NumberOfModules=0
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 400
	#MainFormHeight: 200 
	#MergeLibraries: False	
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private Selenium As Pnd_Selenium
End Sub


	' *******************************************************************************
	' In this example:
	' How to wait for condition to be met
	' How to create timeout in case condition never met
	' *******************************************************************************


Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	Sleep(500) ' To show MainForm before Chrome
			
	Selenium.Initialize("Selenium", "e:\Kotlin\Wrap_Selenium\ChromeForTestingAndChromeDriver\chromedriver.exe")
		
	Selenium.NavigateTo("https://www.selenium.dev/selenium/web/dynamic.html")
End Sub


Sub Selenium_NavigationFinished
	Dim Revealed As Pnd_WebElement = Selenium.FindById("revealed")
	Dim Reveal As Pnd_WebElement = Selenium.FindById("reveal")
	Reveal.Click
			
	Log("Wait begin")
	Dim Counter As Int = 0
	Do While Revealed.IsDisplayed = False    ' we are waiting for Revealed to be displayed
		Sleep(200)                           ' 200 milliseconds, 5 iterations per second
		Counter = Counter + 1
		If Counter >= 10 Then                ' If Counter >= 10, at least 2 seconds has passed (10 iterations with 0.2 seconds each). We need to have some kind of timeout and exit Do While Loop in case condition never met
			Exit
		End If
	Loop
	Log("Wait ends")
	
	If Revealed.IsDisplayed Then
		Log("Send keys: revealed")
		Revealed.SendKeys("revealed")
	Else
		Log("Time out")
	End If
End Sub

