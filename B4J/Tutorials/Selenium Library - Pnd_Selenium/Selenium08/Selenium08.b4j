AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=pnd_selenium
Library4=jstringutils
NumberOfFiles=1
NumberOfLibraries=4
NumberOfModules=0
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 400
	#MainFormHeight: 200 
	#MergeLibraries: False	
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private Selenium As Pnd_Selenium
End Sub


	' *******************************************************************************
	' In this example:
	' How to use ChromeDevtools Protocol (ExecuteCdpCommand) and where to find all possible commands
    ' How to take screenshot of the entire page loaded by the browser with ExecuteCdpCommand
	' How to set different geolocation with ExecuteCdpCommand
	' *******************************************************************************


Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	Sleep(500) ' To show MainForm before Chrome
			
	Selenium.Initialize("Selenium", "e:\Kotlin\Wrap_Selenium\ChromeForTestingAndChromeDriver\chromedriver.exe")
		
	Selenium.NavigateTo("https://where-am-i.org/")
End Sub


Sub Selenium_NavigationFinished
	' Choose options from the menu on left side,
	' below DOMAINS or use search on the top of the page,
	' on this link: https://chromedevtools.github.io/devtools-protocol/tot/Accessibility/
	
	' In CDP documenatation when parameter is Number in B4J that is Double
	

	' CdpCommand: Page.captureScreenshot - screenshot of the entire page loaded by the browser
	' Usage for this CDP Command: https://chromedevtools.github.io/devtools-protocol/tot/Page/#method-captureScreenshot
	Dim Parameters As Map
	Parameters.Initialize
	Parameters.Put("format", "jpeg")
	Parameters.Put("quality", 100)
	Parameters.Put("fromSurface", True)
	Parameters.Put("captureBeyondViewport", True)
	Parameters.Put("optimizeForSpeed", False)
	Dim RetMap As Map = Selenium.ExecuteCdpCommand("Page.captureScreenshot", Parameters)  ' ExecuteCdpCommand with parameters and get Map in return
	Dim su As StringUtils
	Dim DecodedBytes() As Byte = su.decodebase64(RetMap.Get("data"))                      ' value of "data" key is Base64 encoded, we need to decode it
	File.WriteBytes(File.DirApp, "Example.jpg", DecodedBytes)                             ' write bytes to file
	Log("File saved: " & File.DirApp & "\Example.jpg")

	
	' CdpCommand: Emulation.setGeolocationOverride - we will set our Geolocation to Tokyo/Japan
	' When started, allow Chrome for Testing to use "your" location
	' Usage for this CDP Command https://chromedevtools.github.io/devtools-protocol/tot/Emulation/#method-setGeolocationOverride
	Dim Parameters2 As Map
	Parameters2.Initialize
	Parameters2.Put("latitude", 35.652832)      ' Double
	Parameters2.Put("longitude", 139.839478)    ' Double
	Parameters2.Put("accuracy", 1.0)            ' Double
	Selenium.ExecuteCdpCommand("Emulation.setGeolocationOverride", Parameters2) ' ExecuteCdpCommand with parameters
End Sub

