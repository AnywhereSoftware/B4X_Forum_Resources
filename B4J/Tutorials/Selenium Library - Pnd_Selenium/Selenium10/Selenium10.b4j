AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=pnd_selenium
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=0
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 400
	#MainFormHeight: 200 
	#MergeLibraries: False	
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private Selenium As Pnd_Selenium
End Sub


	' *******************************************************************************
	' In this example:
	' How to use Action class
	' Using mouse action: scroll down, click, drag and drop
	' *******************************************************************************


Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	Sleep(500) ' To show MainForm before Chrome
			
	Selenium.Initialize("Selenium", "e:\Kotlin\Wrap_Selenium\ChromeForTestingAndChromeDriver\chromedriver.exe")
			
	Selenium.NavigateTo("https://www.selenium.dev/selenium/web/mouse_interaction.html")
End Sub


Sub Selenium_NavigationFinished
	
	Select Selenium.CurrentUrl
		
		Case "https://www.b4x.com/"
			
			Log ("ActionScroll - START")
			ActionScroll
			Log ("ActionScroll - END")
			
			Sleep(5000)
			Selenium.Quit
			Sleep(500)
			ExitApplication
			
		Case Else
			
			Log ("ActionClick - START")
			ActionClick
			Log ("ActionClick - END")
			Sleep(2000)
			Log ("ActionDragDrop - START")
			ActionDragDrop
			Log ("ActionDragDrop - END")
						
			Sleep(2000)
			Selenium.NavigateTo("https://www.b4x.com")
				
	End Select

End Sub


Sub ActionClick
	Dim KeyCodes As Pnd_KeyCodes
	Dim ClickableWebElement As Pnd_WebElement = Selenium.FindById("clickable")

	Dim Action As Pnd_Action
	Action.Initialize(Selenium.WebDriver)
	Action.MoveToWebElement(ClickableWebElement)       ' move mouse to wWbElement
	Action.Pause(1000)                                 ' pause 1 second (not needed just for this example)
	Action.Click                                       ' click on mouse location
	Action.SendKeys("Test text ")                      ' send text to acive WebElement
	Action.KeyDown((KeyCodes.SHIFT).As(Object))        ' press (and hold) shift key
	Action.SendKeys("a")                               ' send lowercase letter a, because shift is pressed uppercase A will be sent
	Action.KeyUp((KeyCodes.SHIFT).As(Object))          ' release shift key
	Action.Perform                                     ' execute Action
End Sub


Sub ActionDragDrop
	Dim Draggable As Pnd_WebElement = Selenium.FindById("draggable")   ' WebElement to drag
	Dim Droppable As Pnd_WebElement = Selenium.FindById("droppable")   ' WebElement where drop will perform
	
	Dim Action As Pnd_Action
	Action.Initialize(Selenium.WebDriver)
	Action.DragAndDrop(Draggable, Droppable)                           ' Drag and Drop one WebElement to another
	Action.Perform                                                     ' execute Action
End Sub


Sub ActionScroll
	Dim Action As Pnd_Action
	Action.Initialize(Selenium.WebDriver)
	Action.Pause(2000)                      ' pause 2 seconds (not needed just for this example)
	Action.ScrollByAmount(0, 1500)          ' scroll down 1500
	Action.Perform                          ' execute Action
End Sub
