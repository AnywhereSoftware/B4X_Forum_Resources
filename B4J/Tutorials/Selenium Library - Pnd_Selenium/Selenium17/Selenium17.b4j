AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=pnd_selenium
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=0
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 400
	#MainFormHeight: 200 
	#MergeLibraries: False	
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private Selenium As Pnd_Selenium
	Private JsHandlerId As Long
	Private ConsoleHandlerId As Long
End Sub


	' *******************************************************************************
	' In this example:
	' How to enable BiDirectional functionality
	' How to use .AddJavaScriptErrorHandler and .AddConsoleMessageHandler
	' How to use events _ConsoleMessage and _JavaScriptError
	' *******************************************************************************


Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	Sleep(500) ' To show MainForm before Chrome
			
	Selenium.SetCapability("webSocketUrl", True) ' to enable BiDirectional functionality, needed for Selenium.AddJavaScriptErrorHandler and Selenium.AddConsoleMessageHandler
	
	Selenium.Initialize("Selenium", "e:\Kotlin\Wrap_Selenium\ChromeForTestingAndChromeDriver\chromedriver.exe")
		
	JsHandlerId = Selenium.AddJavaScriptErrorHandler
	ConsoleHandlerId = Selenium.AddConsoleMessageHandler
	Selenium.NavigateTo("https://www.selenium.dev/selenium/web/bidi/logEntryAdded.html")
End Sub


Sub Selenium_ConsoleMessage (Text As String, Value As Object, Method As String, ObjType As String)
	Log("Console Text: " & Text)
	If (ObjType = "string") Then
		Log("Console Value: " & Value.As(String))
	End If
	Log("Console Method: " & Method)
	Log("Console ObjType: " & ObjType)
End Sub


Sub Selenium_JavaScriptError (ErrorMessage As String, FunctionName As String, JsUrl As String, LineNumber As Int, ColumnNumber As Int, Time As Long)
	Log("ErrorMessage: " & ErrorMessage)
	Log("FunctionName: " & FunctionName)
	Log("JsUrl: " & JsUrl)
	Log("LineNumber: " & LineNumber)
	Log("ColumnNumber: " & ColumnNumber)
	Log("Time: " & DateTime.Time(Time) & " - " & DateTime.Date(Time))
End Sub


Sub Selenium_NavigationFinished	
	Dim ConsoleLog As Pnd_WebElement = Selenium.FindById("consoleLog")
	ConsoleLog.Click
	Sleep(1000)
	
	Dim ConsoleError As Pnd_WebElement = Selenium.FindById("consoleError")
	ConsoleError.Click
	Sleep(1000)
	
	Dim JsException As Pnd_WebElement = Selenium.FindById("jsException")
	JsException.Click
	Sleep(1000)
	
	Dim LogWithStacktrace As Pnd_WebElement = Selenium.FindById("logWithStacktrace")
	LogWithStacktrace.Click
	Sleep(1000)

	Selenium.RemoveJavaScriptErrorHandler(JsHandlerId)
	Log("Removed JavaScriptErrorHandler with HandlerId: " & JsHandlerId)
	Selenium.RemoveConsoleMessageHandler(ConsoleHandlerId)
	Log("Removed ConsoleMessageHandler with HandlerId: " & ConsoleHandlerId)
	Sleep(2000)
	Selenium.Quit
	Sleep(1000)
	ExitApplication
End Sub
