AppType=JavaFX
Build1=Default,b4j.example
File1=1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jrandomaccessfile
Library4=jserial
Module1=AsyncStreamsText
NumberOfFiles=1
NumberOfLibraries=4
NumberOfModules=1
Version=9.8
@EndOfDesignText@
#Region  Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 500
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Dim btnOpen As Button
	Dim cmbPort As ComboBox
	Dim txtInput As TextField
	Dim txtLog As TextArea
	Dim lblStatus As Label
	Private sp As Serial
	Private astream As AsyncStreamsText
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1") 'Load the layout file.
	MainForm.Show
	MainForm.Title = "Serial port - AsyncStreamsText - Chat"
	MainForm.BackColor = fx.Colors.White
	sp.Initialize("")
	cmbPort.Items.AddAll(sp.ListPorts)
End Sub

Sub cmbPort_SelectedIndexChanged(Index As Int, Value As Object)
	btnOpen.Enabled = Index > -1 'enable the button if there is a selected item
End Sub

Sub btnOpen_Action
	sp.Open(cmbPort.Value)
	'astream.InitializePrefix(sp.GetInputStream, True, sp.GetOutputStream, "astream")
	astream.Initialize(Me,"astream",sp.GetInputStream,sp.GetOutputStream)
	txtInput.Enabled = True
	btnOpen.Enabled = False
	lblStatus.Text = "Status: Open"
End Sub

Sub MainForm_Closed
	astream.Close
	sp.Close
End Sub

'Sub AStream_NewData (Buffer() As Byte)
'	Dim s As String = BytesToString(Buffer, 0, Buffer.Length, "UTF8")
'	LogMessage("You", s)
'End Sub

Sub AStream_NewText (Buffer As String)
	LogMessage("You", Buffer)
End Sub

Public Sub SendData (data As String)
	astream.Write(data)
End Sub

Sub txtInput_Action
	'astream.Write(txtInput.Text.GetBytes("UTF8"))
	SendData(txtInput.Text & Chr(10))
	txtInput.SelectAll
	txtInput.RequestFocus
	LogMessage("Me", txtInput.Text)
End Sub

Sub LogMessage(From As String, Msg As String)
	txtLog.Text = txtLog.Text & From & ": " & Msg & CRLF
	txtLog.SetSelection(txtLog.Text.Length, txtLog.Text.Length)
End Sub

Sub AStream_Error
	Log("Error: " & LastException)
	astream.Close
	AStream_Terminated
End Sub

Sub AStream_Terminated
	Log("Connection is broken.")
	lblStatus.Text = "Status: Close"
	txtInput.Enabled = False
	btnOpen.Enabled = True
End Sub