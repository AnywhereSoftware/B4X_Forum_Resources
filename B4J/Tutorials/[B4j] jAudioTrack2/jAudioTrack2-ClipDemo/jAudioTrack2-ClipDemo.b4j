AppType=JavaFX
Build1=Default,b4j.example
File1=1.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jaudiotrack2-b4xlib
Library2=jcore
Library3=jfx
Library4=jxui
Library5=xui views
NumberOfFiles=1
NumberOfLibraries=5
NumberOfModules=0
Version=9.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region


Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private InitialFilePath As String
	Private FileName As String
	Private BtnFile As Button
	Private lblFileName As Label
	Private btnPlay As B4XView
	Private btnStop As B4XView
	Private btnPause As B4XView
	Private tfURL As TextField
	
	Private Devices As List
	
	Private AIS As JavaObject
	Private Clip1 As ClipWrapper
	
	Private cbOutputs As B4XComboBox
	
	Private SelectedMixerInfo As JavaObject
	Private sbPosition As B4XSeekBar
	Private lblTime As B4XView
	Private lblDuration As B4XView
	
	Private PosTimer As Timer
	
	Private Paused As Boolean
	
	Private Reload As Boolean
End Sub


Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1") 'Load the layout file.
	MainForm.Show
	MainForm.Title = "jAudioTrack2 - ClipDemo"

'	btnPlay.Enabled=False

	'For file dialog
	InitialFilePath = GetSystemProperty("user.home","")
	
	'Setup Device names in a B$XCombobox and MixerInfo objects in a list
	Dim MixerInfos As List = jAudioTrack2_Utils.GetDevices(jAudioTrack2_Utils.DEVICETYPE_OUTPUT,"")
	
	Dim L As List
	L.Initialize
	Devices.Initialize
	For Each MI As JavaObject In MixerInfos
		'Add the name to the list for the B4xCombobox
		L.add(MI.RunMethod("getName",Null))
		'Add the MixerInfo to the corresponding Devices List
		Devices.Add(MI)
	Next
	
	'Set the list for the B4xCombobox
	cbOutputs.SetItems(L)
	cbOutputs_SelectedIndexChanged (0)
	
	PosTimer.Initialize("Postimer",1000)
End Sub

'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
	Return True
End Sub

Private Sub MainForm_CloseRequest (EventData As Event)
	If Clip1.IsInitialized And Clip1.IsRunning Then
		PosTimer.Enabled = False
		Clip1.Stop
	End If
		
End Sub

Sub BtnFile_Click

	'Get a file to play from the user
	Dim FC As FileChooser
	
	FC.Initialize
	FC.Title="Choose a Wav file"
	FC.SetExtensionFilter("Wav files",Array As String("*.wav"))
	FC.InitialDirectory = InitialFilePath
	Dim response As String = FC.ShowOpen(MainForm)
	If response = "" Then Return
	
	If InitialFilePath = File.GetFileParent(response) And FileName =  File.GetName(response) Then
		Return
	End If
	Reload = True
	InitialFilePath = File.GetFileParent(response)
	FileName =  File.GetName(response)
	lblFileName.Text=File.Combine(InitialFilePath,FileName)
	
End Sub


Sub btnStop_Click
	PosTimer.Enabled = False
	Clip1.Stop
	Clip1.SetMicrosecondPosition(0)
	sbPosition.Value = 0
	Paused = False
End Sub

Private Sub btnPause_Click
	Paused = True
	PosTimer.Enabled = False
	Clip1.Stop
End Sub

Sub btnPlay_Click
	
	If Clip1.IsInitialized Then
		If Clip1.IsRunning Then
			Return
		End If
		If Paused Then
			PosTimer.Enabled = True
			Clip1.Start
			Return
		End If
	End If
	
	If Reload = False Then
		PosTimer.Enabled = True
		Clip1.Start
		Return
	End If
	
	Reload = False
	
	If AIS.IsInitialized Then AIS.RunMethod("close",Null)
	
	Dim AIS As JavaObject
	
	If tfURL.Text <> "" Then
		If tfURL.Text.ToLowerCase.StartsWith("http") Then
			AIS = jAudioTrack2_Utils.GetAudioInputStream3(tfURL.Text)
		Else
			AIS = jAudioTrack2_Utils.GetAudioInputStream3(File.GetUri(tfURL.Text,"UTF-8"))
		End If
	Else If FileName <> "" Then

		AIS = jAudioTrack2_Utils.GetAudioInputStream(InitialFilePath,FileName)
		
	End If
	
	If AIS.IsInitialized = False Then Return
	
	Clip1 = jAudioTrack2_Utils.GetClip(SelectedMixerInfo)
	Clip1.Open2(AIS)
	
	Clip1.AddLineListener(Me,"Clip1")
	
	Dim Duration_ms As Long = Clip1.GetMicrosecondLength / 1000
	lblDuration.Text = ConvertMillisecondsToString(Duration_ms)
	
	sbPosition.MinValue = 0
	sbPosition.MaxValue = Duration_ms
	sbPosition.Value = 0
	
	PosTimer.Enabled = True
	Clip1.Start
End Sub

Private Sub Clip1_Event(Name As String)
	'You can use the event to change the gui if needed. Play/Stop button maybe
	Log("Clip1Event " & Name)
End Sub

Private Sub cbOutputs_SelectedIndexChanged (Index As Int)
	
	If Clip1.IsInitialized And Clip1.IsRunning Then
		PosTimer.Enabled = False
		Clip1.Stop
		Clip1.SetMicrosecondPosition(0)
		sbPosition.Value = 0
	End If
	
	'Get the selected Mixer Object from the Devices List
	SelectedMixerInfo = Devices.Get(Index)
	
	Log("Changed " & cbOutputs.GetItem(Index))
	
End Sub

Private Sub PosTimer_Tick
	Dim Pos As Long = Clip1.GetMicrosecondPosition / 1000
	sbPosition.Value = Pos
	lblTime.Text = ConvertMillisecondsToString(Pos)
End Sub

Sub ConvertMillisecondsToString(t As Long) As String
	Dim hours, minutes, seconds As Int
	hours = t / DateTime.TicksPerHour
	minutes = (t Mod DateTime.TicksPerHour) / DateTime.TicksPerMinute
	seconds = (t Mod DateTime.TicksPerMinute) / DateTime.TicksPerSecond
	Return $"$1.0{hours}:$2.0{minutes}:$2.0{seconds}"$
End Sub

Private Sub tfURL_FocusChanged (HasFocus As Boolean)
	If HasFocus Then
		tfURL.Tag = tfURL.Text
	Else
		If tfURL.Text <> tfURL.Tag Then
			Reload = True
		End If
	End If
End Sub

Private Sub sbPosition_ValueChanged (Value As Int)
	Clip1.SetMicrosecondPosition(Value * 1000)
	PosTimer_Tick
End Sub


Private Sub btnBack_Click
	If Clip1.AsJavaObject.IsInitialized And Clip1.IsRunning Then
		Clip1.SetMicrosecondPosition(Clip1.GetMicrosecondPosition + Skip("back"))
		PosTimer_Tick
	End If
End Sub

Private Sub btnFwd_Click
	If Clip1.AsJavaObject.IsInitialized And Clip1.IsRunning Then
		Clip1.SetMicrosecondPosition(Clip1.GetMicrosecondPosition + Skip("fwd"))
		PosTimer_Tick
	End If
End Sub

Private Sub Skip(Dir As String) As Long
	Dim TenSeconds As Long = 10000000
	Return IIf(Dir = "fwd",TenSeconds,-TenSeconds)
End Sub