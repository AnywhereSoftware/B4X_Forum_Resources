AppType=JavaFX
Build1=Default,b4j.example
File1=1.bjl
File2=couch.png
File3=Item.bjl
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jxui
Library4=jbitmapcreator
Library5=xui views
Library6=xcustomlistview
Module1=|absolute|C:\Users\H\Documents\B4J\BitmapCreator\BitmapCreatorEffects\BitmapCreatorEffects
NumberOfFiles=3
NumberOfLibraries=6
NumberOfModules=1
Version=7.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 800
	#MainFormHeight: 600 
#End Region

'Icon source: Font Awesome Free 5.2.0 by @fontawesome - https://fontawesome.com
Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private ChairsPanel As B4XView
	Private lst As CustomListView
	Private xui As XUI
	Private ChairsImageViews(120) As ImageView
	Private BlackCouch, GreenCouch, RedCouch As B4XBitmap
	Private CurrentNumberOfSelectedChairs As Int
	Private TargetNumberOfSelectedChairs As Int
	Private UpdateLoopIndex As Int
	Private lblCounter As B4XView
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1") 
	MainForm.Show
	FillPartiesList
	lst.sv.SetColorAndBorder(xui.Color_Transparent, 0, 0, 0)
	CreateChairsImageViews
End Sub

Sub CreateChairsImageViews
	Dim gap As Int = 4dip
	Dim w As Int = ChairsPanel.Width / 12 - gap
	Dim h As Int = ChairsPanel.Height / 10 - gap
	BlackCouch = xui.LoadBitmapResize(File.DirAssets, "couch.png", w, h, True)
	Dim effects As BitmapCreatorEffects
	effects.Initialize
	GreenCouch = effects.ReplaceColor(BlackCouch, xui.Color_Black, 0xFF228200, True)
	RedCouch = effects.ReplaceColor(BlackCouch, xui.Color_Black, 0xFFAE0000, True)
	Dim count As Int
	For y = 0 To 9
		For x = 0 To 11
			Dim iv As ImageView
			iv.Initialize("")
			ChairsPanel.AddView(iv, (w + gap) * x, (h + gap) * y, w, h)
			iv.SetImage(BlackCouch)
			ChairsImageViews(count) = iv
			count = count + 1
		Next
	Next
End Sub

Sub FillPartiesList
	Dim NumberOfParties As Int = 7
	Dim total As Int
	Dim n(NumberOfParties) As Int
	For i = 0 To NumberOfParties - 1
		n(i) = Rnd(0, 100)
		total = total + n(i)
	Next
	Dim divider As Float = 120 / total
	total = 0
	For i = 0 To NumberOfParties - 1
		If i < NumberOfParties - 1 Then
			n(i) = Floor(n(i) * divider)
			total = total + n(i)
		Else
			n(i) = 120 - total
		End If
	Next
	For i = 0 To NumberOfParties - 1
		Dim p As B4XView = xui.CreatePanel("")
		p.SetLayoutAnimated(0, 0, 0, lst.AsView.Width, 45dip)
		p.LoadLayout("Item")
		p.GetView(1).Text = $"Party ${Chr(Asc("A") + i)} - ${n(i)}"$
		lst.Add(p, n(i))
	Next
End Sub

Sub B4XSwitch1_ValueChanged (Value As Boolean)
	Dim switch As B4XSwitch = Sender
	Dim item As Int = lst.GetItemFromView(switch.mBase)
	Dim chairs As Int = lst.GetValue(item)
	If Value Then 
		TargetNumberOfSelectedChairs = TargetNumberOfSelectedChairs + chairs
	Else
		TargetNumberOfSelectedChairs = TargetNumberOfSelectedChairs - chairs
	End If
	UpdateLoop
End Sub

Sub UpdateLoop
	UpdateLoopIndex = UpdateLoopIndex + 1
	Dim MyIndex As Int = UpdateLoopIndex
	Do While MyIndex = UpdateLoopIndex And CurrentNumberOfSelectedChairs <> TargetNumberOfSelectedChairs
		If TargetNumberOfSelectedChairs > CurrentNumberOfSelectedChairs Then
			CurrentNumberOfSelectedChairs = CurrentNumberOfSelectedChairs + 1
			Dim iv As ImageView = ChairsImageViews(CurrentNumberOfSelectedChairs - 1)
			If CurrentNumberOfSelectedChairs = 61 Then
				For i = 0 To 59
					ChairsImageViews(i).SetImage(GreenCouch)
				Next
			End If
			If CurrentNumberOfSelectedChairs > 60 Then
				iv.SetImage(GreenCouch)
			Else
				iv.SetImage(RedCouch)
			End If
			
		Else
			CurrentNumberOfSelectedChairs = CurrentNumberOfSelectedChairs - 1
			Dim iv As ImageView = ChairsImageViews(CurrentNumberOfSelectedChairs)
			If CurrentNumberOfSelectedChairs = 60 Then
				For i = 0 To 59
					ChairsImageViews(i).SetImage(RedCouch)
				Next
			End If
			iv.SetImage(BlackCouch)
		End If
		lblCounter.Text = CurrentNumberOfSelectedChairs
		Sleep(30)
	Loop
End Sub







