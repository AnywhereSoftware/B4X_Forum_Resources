AppType=StandardJava
Build1=Default,b4j.example
Group=Default Group
Library1=jcore
Library2=jserver
Library3=jsql
Library4=easyhtml
Module1=ControllerInicio
Module2=ModelAddTask
Module3=ModelDeleteTask
Module4=ModelGetTasks
NumberOfFiles=0
NumberOfLibraries=4
NumberOfModules=4
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#CommandLineArgs:
	#MergeLibraries: True 
#End Region

#AdditionalJar: sqlite-jdbc-3.7.2

Sub Process_Globals
	Private srvr As Server
	
	Public db As SQL
End Sub

Sub AppStart (Args() As String)
	srvr.Initialize("srvr")
	srvr.Port = 51042
	srvr.StaticFilesFolder = File.Combine(File.DirApp, "www")
	srvr.AddHandler("", "ControllerInicio", False)
	srvr.AddHandler("/get", "ModelGetTasks", False)
	srvr.AddHandler("/add", "ModelAddTask", False)
	srvr.AddHandler("/delete", "ModelDeleteTask", False)
	srvr.Start
	StartMessageLoop
	'open browser and navigate to: http://127.0.0.1:51042/
End Sub

Public Sub OpenDB
	db.InitializeSQLite(File.DirApp, "dados.sqlite3", True)
End Sub

Public Sub GetListTask As String
	Dim Ul As EasyHTML
	Ul.Initialize("ul").AddStyle("margin-top", "3rem")
	Try
		OpenDB
		Dim Cursor As ResultSet = db.ExecQuery("SELECT * FROM tasks")
		Do While Cursor.NextRow
			Dim BtnDelete As EasyHTML
			Dim li As EasyHTML
			BtnDelete.Initialize("button").HX_POST("/delete").HX_Vals($" { "id": "${Cursor.GetString("id")}" } "$).HX_Target("#divTasks").AddElement("Delete").AddClass("secondary")
			li.Initialize("li").AddElement(Cursor.GetString("task")).AddElement(BtnDelete.Mount).AddStyle("display", "flex").AddStyle("justify-content", "space-between")
			Ul.AddElement(li.Mount)
		Loop
		db.Close
	Catch
		Log(LastException)
	End Try
	Return Ul.Mount
End Sub
