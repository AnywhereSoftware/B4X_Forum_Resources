AppType=JavaFX
Build1=Default,OpenPDF_Demo.B4J
File1=esempio.jpg
File2=Layout1.bjl
File3=napoli.jpg
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jxui
Library4=javaobject
Module1=clsOpenPDF
NumberOfFiles=3
NumberOfLibraries=4
NumberOfModules=1
Version=10.2
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
	'#AdditionalJar: openpdf-1.3.30.jar
	#AdditionalJar: openpdf-2.0.3.jar
	'please, download openpdf.jar and save it into b4j Libraries (Anywhere Software\B4J\Libraries)
	'https://central.sonatype.com/artifact/com.github.librepdf/openpdf/versions
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI
	Private Button1 As B4XView
	Dim p As clsOpenPDF
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	MainForm.Show
	'
	'Larghezza A4: 595 punti
	'Altezza A4: 842 punti
	'Queste dimensioni sono standard nel formato A4
	'Un foglio A4 ha le dimensioni di 210 x 297 millimetri.
	'In punti tipografici, le dimensioni sono 595 x 842
End Sub
'
Private Sub Button1_Click
	prova
End Sub
'
Private Sub prova
	' Definisci il percorso e il nome del file PDF
	Dim img1 As String = "esempio.jpg"
	Dim img2 As String = "napoli.jpg"
	If File.Exists(File.DirApp, img1) = False Then
		File.Copy(File.DirAssets, img1, File.DirApp, img1)
	End If
	If File.Exists(File.DirApp, img2) = False Then
		File.Copy(File.DirAssets, img2, File.DirApp, img2)
	End If
	Dim outputFile As String = DateTime.Now & ".pdf"
	Dim outputPath As String = File.DirApp
	' Inizializza il PDF
	p.Initialize(outputPath, outputFile)
	p.ImpostaMetadati("Il titolo", "Me medesimo", "Oggetto", "Simm'e Napule Paisà")
	' Aggiungi alcuni testi di esempio
	For f = 5 To 150 Step 10
		Dim assex As Int = Rnd(10, 400)
		Dim s As String = $"Asse X=${assex} Asse Y=${f}"$
		rndColore
		p.AddText(assex, f, s)
	Next
	p.thickness = 2
	' Disegna una linea retta
	rndColore
	p.DisegnaLinea(50, 160, 123, 160)
	
	' Disegna una linea obbligua
	rndColore
	p.DisegnaLinea(180, 160, 250, 190)
	p.thickness = 0.5
	' Aggiungi un'immagine
	p.addImage(File.DirApp, img1)

	' Aggiungi un'altra immagine con posizione personalizzata
	p.addImage2(File.DirApp, img2, 10, 400)

	' Aggiungi testo aggiuntivo
	Dim oldfontSize As Float = p.fontSize
	p.fontSize = 20
	p.Colore = xui.Color_Blue
	p.AddText(280, 480, "Forza Napoli! Sempre")', 20, 0, 153, 255, 0)
	p.Colore = xui.Color_Black
	p.fontSize = oldfontSize
	' Nuova pagina
	p.NuovaPagina

	' Disegna un cerchio e un rettangolo
	rndColore
	p.DisegnaCerchio(140, 200, 50, 0.5) ', 220, 123, 150)
	rndColore
	'p.DisegnaRettangolo(80, 80, 50, 100) ', 0.3 , 210, 20, 30)
	p.DisegnaRettangoloArrotondato(80, 80, 50, 100, 10)
	' Nuova pagina e testo verticale
	p.NuovaPagina
	rndColore
	p.AddTextVerticale(100, 80, "Verticale alto", False)
	rndColore
	p.AddTextVerticale(200, 180, "Verticale basso", True)

	' Aggiungi la tabella
	Tabella
	p.AddEAN13(100, 500, "808182838481", 90, 30)
	
	p.AddEAN13(100, 400, "789456123078", 90, 30)
	'
	p.FontSize = 7
	p.AddEAN13(200, 500, "808182838481", 60, 25)
	p.AddEAN13(200, 400, "789456123078", 50, 20)
	p.FontSize = 10

	' Salva e mostra il PDF
	p.SaveANDShow(True)
End Sub

Private Sub Tabella
	' Definisci le larghezze delle colonne
	rndColore
	Dim columnWidths As List
	columnWidths.Initialize
	columnWidths.Add(100) ' Larghezza della prima colonna
	columnWidths.Add(150) ' Larghezza della seconda colonna
	columnWidths.Add(200) ' Larghezza della terza colonna

	' Definisci le intestazioni delle colonne
	Dim headers As List
	headers.Initialize
	headers.Add("Colonna 1")
	headers.Add("Colonna 2")
	headers.Add("Colonna 3")

	' Definisci l'altezza e lo spessore delle linee
	Dim lineHeight As Float = 600 ' Altezza totale della pagina (può variare)

	' Imposta la posizione iniziale della tabella
	Dim asseX As Float = 50 ' Posizione iniziale sulla X
	Dim asseY As Float = 700 ' Posizione iniziale sulla Y (più in basso sulla pagina)

	' Aggiungi la tabella al PDF
	p.AddTable(asseX, asseY, 3, columnWidths, headers, lineHeight)
	'aggiunge contenuto nella tabella
	p.AddText(asseX + 5, asseX + lineHeight + 5 , DateTime.Time(DateTime.Now))', 10, 139, 69, 19, 0)
	Dim nStep As Int = -15
	p.Allineamento = 2 'allinea a destra
	For l = 1 To 7
		Dim numberF As String = NumberFormat2(Rnd(12345, 78912) / 61, 1, 2, 2, True)
		rndColore
		p.fontSize = Rnd(6, 12)
		p.AddText(asseX + columnWidths.Get(0) + columnWidths.Get(1) - 5, asseX + lineHeight - nStep, numberF)
		nStep = nStep + 10
	Next
End Sub
'
Private Sub rndColore
	Dim r As Int = Rnd(0, 255)
	Dim g As Int = Rnd(0, 255)
	Dim b As Int = Rnd(0, 255)
	Dim alpha As Int = 255 ' Opaco
	p.Colore = Bit.Or(Bit.ShiftLeft(alpha, 24), Bit.Or(Bit.ShiftLeft(r, 16), Bit.Or(Bit.ShiftLeft(g, 8), b)))
End Sub
