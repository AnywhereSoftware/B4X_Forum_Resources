Version=2.2
AppType=JavaFX
IconFile=
NumberOfModules=3
Module1=WebSocketHandler
Module2=PushService
Module3=KeyValueStore
Build1=Default,b4j.example
NumberOfFiles=1
File1=1.fxml
NumberOfLibraries=6
Library1=jcore
Library2=jfx
Library3=json
Library4=jsql
Library5=jwebsocketclient
Library6=jrandomaccessfile
@EndOfDesignText@
#Region  Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 400 
#End Region

#AdditionalJar: sqlite-jdbc-3.7.2
Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private lblId As Label
	Private lblStatus As Label
	Private txtMessages As TextArea
	Public serverLink As String = "ws://basic4ppc.com:51042/push/b4a_ws2"
	Private txtSendMessage As TextField
	Private btnSendMessage As Button
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("1")
	MainForm.Show
	PushService.Create
End Sub

Public Sub UpdateId (id As String)
	lblId.Text = "id: " & id
End Sub

Public Sub UpdateStatus(status As Boolean)
	If status Then lblStatus.Text = "Connected" Else lblStatus.Text = "Disconnected"
	btnSendMessage.Enabled = status
	txtSendMessage.Enabled = status
End Sub

Public Sub NewMessage(Messages As List)
	For Each msg As Message In Messages
		txtMessages.Text = txtMessages.Text & "Message Id: " & msg.id & ", " & msg.Text & CRLF
	Next
	txtMessages.SetSelection(txtMessages.Text.Length, txtMessages.Text.Length)
End Sub

Sub MainForm_Closed
	PushService.Close
End Sub

Sub btnSendMessage_Action
	If txtSendMessage.Text.Trim.Length > 0 Then
		PushService.SendMessageToCloud("(from B4J app) " & txtSendMessage.Text)
		txtSendMessage.SelectAll
		txtSendMessage.RequestFocus
	End If
End Sub

Sub txtSendMessage_Action
	btnSendMessage_Action
End Sub